*** Variables ***
${NO_INBAND_SECURITY}  0
${DIAMETER_SUCCESS}  2001
${NO_STATE_MAINTAINED}  1

*** Keywords ***
Add Message Version
    [Arguments]  ${version}
    Add Hex Data  ${version}  1

Add Message Length
    [Arguments]  ${length}
    Add Decimal As Octets  ${length}  3

Add Message Flags
    [Arguments]  ${flags}
    Add Hex Data  ${flags}  1

Add Command Code
    [Arguments]  ${cc}
    Add Decimal As Octets  ${cc}  3

Add Application Id
    [Arguments]  ${ai}
    Add Decimal As Octets  ${ai}  4

Add Hop-By-Hop Identifier
    [Arguments]  ${hbhi}
    Add Hex Data  ${hbhi}  4

Add End-To-End Identifier
    [Arguments]  ${e2ei}
    Add Hex Data  ${e2ei}  4

Add AVP: Origin-Host
    [Arguments]  ${host}
    ${length}=  Get Length  ${host}
    Add AVP Code And Flags  ${avp_code.origin_host}  0x40
    Add AVP Length  ${length + 8}
    Add String  ${host}
    Add Padding After String Data  ${length}

Add AVP Code
    [Arguments]  ${code}
    Add Decimal As Octets  ${code}  4

Add AVP Flags
    [Arguments]  ${flags}
    Add Hex Data  ${flags}  1

Add AVP Code And Flags
    [Arguments]  ${code}  ${flags}
    Add AVP Code  ${code}
    Add AVP Flags  ${flags}

Add AVP Length
    [Arguments]  ${length}
    Add Decimal As Octets  ${length}  3

Add AVP: Origin-Realm
    [Arguments]  ${realm}
    ${length}=  Get Length  ${realm}
    Add AVP Code And Flags  ${avp_code.origin_realm}  0x40
    Add Avp Length  ${length + 8}
    Add String  ${realm}
    Add Padding After String Data  ${length}

Add AVP: Host-IP-Address
    [Arguments]  ${ip}
    Add AVP Code And Flags  ${avp_code.host_ip_address}  0x40
    Add AVP Length  14
    Add Host Ip Address  1  ${ip}
    Add Decimal As Octets  0  2  # delimiter / spare(?)

Add Host Ip Address
    [Arguments]  ${family}  ${ip}
    Add Decimal As Octets  ${family}  2
    Add Ip As Hex  ${ip}

Add AVP: Vendor-Id
    [Arguments]  ${id}
    Add AVP Code And Flags  ${avp_code.vendor_id}  0x40
    Add AVP Length  12
    Add Vendor-Id  ${id}

Add Vendor-Id
    [Arguments]  ${id}
    Add Decimal As Octets  ${id}  4

Add AVP: Product-Name
    [Arguments]  ${product_name}
    ADD AVP Code And Flags  ${avp_code.product_name}  0x00
    ${length}=  Get Length  ${product_name}
    Add AVP Length  ${length + 8}
    Add String  ${product_name}
    Add Padding After String Data  ${length}

Add AVP: Supported-Vendor-Id
    [Arguments]  ${id}
    Add AVP Code And FLags  ${avp_code.supported_vendor_id}  0x40
    Add Avp Length  12
    Add Supported-Vendor-Id  ${id}

Add Supported-Vendor-Id
    [Arguments]  ${id}
    Add Decimal As Octets  ${id}  4

Add AVP: Vendor-Specific-Application-Id
    [Arguments]  ${avp_vendor_id}  ${auth_application_id}
    Add AVP Code And Flags  ${avp_code.vendor_specific_application_id}  0x40
    Add AVP Length  32
    Add AVP: Vendor-Id  ${avp_vendor_id}
    Add AVP: Auth-Application-Id  ${auth_application_id}

Add AVP: Auth-Application-Id
    [Arguments]  ${id}
    Add AVP Code And Flags  ${avp_code.auth_application_id}  0x40
    Add AVP Length  12
    Add Auth-Application-Id  ${id}

Add Auth-Application-Id
    [Arguments]  ${id}
    Add Decimal As Octets  ${id}  4

Add AVP: Inband-Security-Id
    [Arguments]  ${id}
    Add AVP Code And Flags  ${avp_code.inband_security_id}  0x40
    Add AVP Length  12
    Add Inband-Security-Id  ${id}

Add Inband-Security-Id
    [Arguments]  ${id}
    Add Decimal As Octets  ${id}  4

Add Firmware-Revision
    [Arguments]  ${fw_rev}
    Add Decimal As Octets  ${fw_rev}  4

Add AVP: Firmware-Revision
    [Arguments]  ${fw_rev}
    Add AVP Code And Flags  ${avp_code.firmware_revision}  0x00
    Add AVP Length  12
    Add Firmware-Revision  ${fw_rev}

Add AVP: Result-Code
    [Arguments]  ${code}
    Add Avp Code And Flags  ${avp_code.result_code}  0x40
    Add AVP Length  12
    Add Result-Code  ${code}

Add Result-Code
    [Arguments]  ${code}
    Add Decimal As Octets  ${code}  4

Add Padding After String Data
    [Arguments]  ${length}  # the length of string which is followed by padding
    [Documentation]  Diameter protocol has 32 bit wide fields. As we enter string data it might cause packet malformation if length of the string is not divisible by four octets - that is is 32 bits. in order to prevent this we add or read extra padding after string if necessary.
    ${padding}=  Evaluate  (4-${length%4})%4
    Add Decimal As Octets  0  ${padding}

Add AVP: Session-ID
    [Arguments]  ${session_id}
    Add AVP Code And Flags  ${avp_code.session_id}  0x40
    ${length}=  Get Length  ${session_id}
    Add AVP Length  ${length + 8}
    Add String  ${session_id}
    Add Padding After String Data  ${length}

Add AVP: Auth-Session-State
    [Arguments]  ${session_state}
    Add AVP Code And Flags  ${avp_code.auth_session_state}  0x40
    Add AVP Length  12
    Add Auth-Session-state  ${session_state}

Add Auth-Session-state
    [Arguments]  ${state}
    Add Decimal As Octets  ${state}  4

Add AVP: Destination-Host
    [Arguments]  ${destination_host}
    Add AVP Code And Flags  ${avp_code.destination_host}  0x40
    ${length}=  Get Length  ${destination_host}
    Add AVP Length  ${length + 8}
    Add String  ${destination_host}
    Add Padding After String Data  ${length}

Add AVP: Destination-realm
    [Arguments]  ${destination_realm}
    Add AVP Code and Flags  ${avp_code.destination_realm}  0x40
    ${length}=  Get Length  ${destination_realm}
    Add AVP Length  ${length + 8}
    Add String  ${destination_realm}
    Add Padding After String Data  ${length}

Add AVP: User-Name
    [Arguments]  ${user_name}
    Add AVP Code And Flags  ${avp_code.user_name}  0x40
    ${length}=  Get Length  ${user_name}
    Add AVP Length  ${length + 8}
    Add String  ${user_name}
    Add Padding After String Data  ${length}

Add AVP: Requested-EUTRAN-Authentication-Info
    [Arguments]  ${number_of_requested_vectors}  ${immediate_response}
    Add AVP Code And Flags  ${avp_code.requested_eutran_authentication_info}  0xc0
    Add AVP Length  44
    Add Decimal As Octets  ${avp_vendor_id.3gpp}  4
    Add Requested-EUTRAN-Authentication-info  ${number_of_requested_vectors}  ${immediate_response}

Add Requested-EUTRAN-Authentication-info
    [Arguments]  ${number_of_requested_vectors}  ${immediate_response_preferred}
    Add AVP: Number-Of-Requested-Vectors  ${number_of_requested_vectors}
    Add AVP: Immediate-Response-Preferred  ${immediate_response_preferred}

Add AVP: Number-Of-Requested-Vectors
    [Arguments]  ${number_of_requested_vectors}
    Add AVP Code And Flags  ${avp_code.number_of_requested_vectors}  0xc0
    Add AVP Length  16
    Add Decimal As Octets  ${avp_vendor_id.3gpp}  4
    Add Decimal As Octets  ${number_of_requested_vectors}  4

Add AVP: Immediate-Response-Preferred
    [Arguments]  ${immediate_response_preferred}
    Add AVP Code And Flags  ${avp_code.immediate_response_preferred}  0xc0
    Add AVP Length  16
    Add Decimal As Octets  ${avp_vendor_id.3gpp}  4
    Add Decimal as Octets  ${immediate_response_preferred}  4

Add AVP: Visited-PLMN-Id
    [Arguments]  ${visited_plmn_id}
    Add AVP Code and Flags  ${avp_code.visited_plmn_id}  0xc0
    ${length}=  Get Length  ${visited_plmn_id}
    Add AVP Length  ${length/2 + 12}
    Add Decimal As Octets  ${avp_vendor_id.3gpp}  4
    Add visited-plmn-id  ${visited_plmn_id}

Add visited-plmn-id
    [Arguments]  ${visited_plmn_id}
    ${length}=  Get Length  ${visited_plmn_id}
    Add Hex Data  ${visited_plmn_id}  ${length/2}
    Add Padding After String Data  ${length/2}

Add AVP: Route-Record
    [Arguments]  ${route_record}
    Add AVP Code And Flags  ${avp_code.route_record}  0x40
    ${length}=  Get Length  ${route_record}
    Add AVP Length  ${length + 8}
    Add String  ${route_record}
    Add Padding After String Data  ${length}

Add AVP: Authentication-Info
    Add AVP Code and Flags  ${avp_code.authentication_info}  0xc0
    Add AVP Length  168
    Add decimal As Octets  ${avp_vendor_id.3gpp}  4
    Add Authentication-Info

Add Authentication-Info
    Add AVP: E-UTRAN-Vector

Add AVP: E-UTRAN-Vector
    Add AVP Code And Flags  ${avp_code.e_utran_vector}  0xc0
    Add Avp Length  156
    Add Decimal As Octets  ${avp_vendor_id.3gpp}  4
    Add E-UTRAN-Vector

Add E-UTRAN-Vector
    Add AVP: Item-Number  1
    Add AVP: RAND  63c619b4ae986823d79234cf0073e550
    Add AVP: XRES  72e42af0fbfe1fabc6b0078b55159251
    Add AVP: AUTN  01021104040060001102304400626500
    Add AVP: KASME  1234567890123456789012345678901234567890123456789012345678901234

Add AVP: Item-Number
    [Arguments]  ${item_number}
    Add AVP Code and Flags  ${avp_code.item_number}  0xc0
    Add AVP Length  16
    Add Decimal As Octets  ${avp_vendor_id.3gpp}  4
    Add Decimal As Octets  ${item_number}  4

Add AVP: RAND
    [Arguments]  ${rand}
    Add AVP Code and Flags  ${avp_code.rand}  0xc0
    ${length}=  Get Length  ${rand}
    Add AVP Length  ${length/2 + 12}
    Add Decimal As Octets  ${avp_vendor_id.3gpp}  4
    Add Hex Data  ${rand}  ${length/2}
    Add Padding After String Data  ${length/2}

Add AVP: XRES
    [Arguments]  ${xres}
    Add AVP Code and Flags  ${avp_code.xres}  0xc0
    ${length}=  Get Length  ${xres}
    Add AVP Length  ${length/2 + 12}
    Add Decimal As Octets  ${avp_vendor_id.3gpp}  4
    Add Hex Data  ${xres}  ${length/2}
    Add Padding After String Data  ${length/2}

Add AVP: AUTN
    [Arguments]  ${autn}
    Add AVP Code and Flags  ${avp_code.autn}  0xc0
    ${length}=  Get Length  ${autn}
    Add AVP Length  ${length/2 + 12}
    Add Decimal As Octets  ${avp_vendor_id.3gpp}  4
    Add Hex Data  ${autn}  ${length/2}
    Add Padding After String Data  ${length/2}

Add AVP: KASME
    [Arguments]  ${kasme}
    Add AVP Code and Flags  ${avp_code.kasme}  0xc0
    ${length}=  Get Length  ${kasme}
    Add AVP Length  ${length/2 + 12}
    Add Decimal As Octets  ${avp_vendor_id.3gpp}  4
    Add Hex Data  ${kasme}  ${length/2}
    Add Padding After String Data  ${length/2}

Add AVP: URL-Flags
    [Arguments]  ${flags}
    Add AVP Code and Flags   ${avp_code.url_flags}  0xc0
    Add AVP Length  16
    Add Decimal As Octets  ${avp_vendor_id.3gpp}  4
    add decimal as octets  ${flags}  4

Add AVP: RAT-Type
    [Arguments]  ${rat_type}
    Add AVP Code and Flags  ${avp_code.rat_type}  0x80
    Add AVP Length  16
    Add Decimal As Octets  ${avp_vendor_id.3gpp}  4
    Add Decimal As Octets  ${rat_type}  4

