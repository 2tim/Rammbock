*** Variables ***
${MCC}  262
${MNC}  12

*** Keywords ***
Set Create Session Request
    Add Flags  72  # Version:2 P: 0 T: 1
    Add Message Type  32  # Create Session Request
    Add Message Length  216
    Add Tunnel Endpoint Identifier  0
    Add Sequence Number  48
    Add Spare Octets  1
    Add International Mobile Subscriber Identity (IMSI)  262120000000001
    Add MSISDN  358  6100000000001
    Add User Location Info (ULI)  ${MCC}  ${MNC}
    Add Serving Network  ${MCC}  ${MNC}
    Add RAT Type
    Add Indication
    Add Fully Qualified Tunnel Endpoint Identifier (F-TEID)  50331665  192.168.112.10
    Add Fully Qualified Tunnel Endpoint Identifier (F-TEID)  0  10.102.182.184
    Add Access Point Name (APN)
    Add Selection Mode
    Add PDN Type
    Add PDN Address Allocation (PAA)
    Add APN Restriction
    Add Aggregate Maximum Bit Rate (AMBR)
    Add Bearer Context : [Grouped IE]
    Add Recovery (Restart Counter)
    Add UE Time Zone
    Add Charging Characteristics

Validate Create Session Request
    Validate Flags  72  # Version:2 P: 0 T: 1
    Validate Message Type  32  # Create Session Request
    Validate Message Length  216
    Validate Tunnel Endpoint Identifier  0
    Validate Sequence Number  48
    Read From Data  1  # Read spare octets
    Validate International Mobile Subscriber Identity (IMSI)  262120000000001
    Validate MSISDN  358  6100000000001
    Validate User Location Info (ULI)  ${MCC}  ${MNC}
    Validate Serving Network  ${MCC}  ${MNC}
    Validate RAT Type
    Validate Indication
    Validate Fully Qualified Tunnel Endpoint Identifier (F-TEID)  50331665  192.168.112.10
    Validate Fully Qualified Tunnel Endpoint Identifier (F-TEID)  0  10.102.182.184
    Validate Access Point Name (APN)
    Validate Selection Mode
    Validate PDN Type
    Validate PDN Address Allocation (PAA)
    Validate APN Restriction
    Validate Aggregate Maximum Bit Rate (AMBR)
    Validate Bearer Context : [Grouped IE]
    Validate Recovery (Restart Counter)
    Validate UE Time Zone
    Validate Charging Characteristics

Add Flags
    [Arguments]  ${flags}
    Add Decimal as Octets  ${flags}  1

Add Message Type
    [Arguments]  ${type}
    Add Decimal as Octets  ${type}  1

Add Message Length
    [Arguments]  ${length}
    Add Decimal as Octets  ${length}  2

Add Tunnel Endpoint Identifier
    [Arguments]  ${id}
    Add Decimal as Octets  ${id}  4

Add Sequence Number
    [Arguments]  ${no}
    Add Decimal as Octets  ${no}  3

Add Spare Octets
    [Arguments]  ${amount}
    Add Decimal as Octets  0  ${amount}

Add International Mobile Subscriber Identity (IMSI)
    [Arguments]  ${imsi}
    Add Decimal as Octets  1  1  # IE Type: International Mobile Subscriber Identity (IMSI) (1)
    Add Decimal as Octets  8  2  # IE Length: 8
    Add Decimal as Octets  0  1  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    Add Number as TBCD  ${imsi}  ${EMPTY}  # IMSI(International Mobile Subscriber Identity number): 262120000000001

Add MSISDN
    [Arguments]  ${country_code}  ${address_digits}
    Add Decimal as Octets  76  1  # IE Type: MSISDN (76)
    Add Decimal as Octets  8  2  # IE Length: 8
    Add Spare Bits  4
    Add Decimal as Bits  0  4  # Instance: 0
    Add Number As TBCD  ${country_code}  ${address_digits}

Add User Location Info (ULI)
    [Arguments]  ${mcc}  ${mnc}
    Add Decimal as Octets  86  1  # IE Type: User Location Info (ULI) (86)
    Add Decimal as Octets  13  2  # IE Length: 13
    Add Decimal as Bits  0  3  # 000. .... = CR flag
    Add Spare Bits  1
    Add Decimal As Bits  0  4  # .... 0000 = Instance: 0
    Add Spare Bits  3
    Add Decimal as Bits  24  5  # ...1 .... = ECGI Present Flag): True, .... 1... = TAI Present Flag): True, .... .0.. = RAI Present Flag): False, .... ..0. = SAI Present Flag): False, .... ...0 = CGI Present Flag): False
    Add Tracking Area Identity(TAI)  ${mcc}  ${mnc}
    Add E-UTRAN Cell Global Identifier (ECGI)  ${mcc}  ${mnc}

Add Spare Bits
    [Arguments]  ${length}
    Add Decimal as Bits  0  ${length}

Add Tracking Area Identity(TAI)
    [Arguments]  ${mcc}  ${mnc}
    add number as tbcd  ${mcc}  # Mobile Country Code (MCC): Germany (Federal Republic of) (262)
    Add Number as tbcd  ${mnc}  # Mobile Network Code (MNC): Dolphin Telecom (Deutschland) GmbH (12)
    Add Decimal as Octets  1  2  # Tracking Area Code: 1

Add E-UTRAN Cell Global Identifier (ECGI)
    [Arguments]  ${mcc}  ${mnc}
    Add Number as tbcd  ${mcc}  # Mobile Country Code (MCC): Germany (Federal Republic of) (262)
    Add Number as tbcd  ${mnc}  # Mobile Network Code (MNC): Dolphin Telecom (Deutschland) GmbH (12)
    Add Decimal as Octets  234  4  # Spare: 0, ECI (E-UTRAN Cell Identifier): 234

Add Serving Network
    [Arguments]  ${mcc}  ${mnc}
    Add Decimal as Octets  83  1  # IE Type: Serving Network (83)
    Add Decimal as Octets  3  2  # IE Length: 3
    Add Decimal as Octets  0  1  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    Add Number as tbcd  ${mcc}  # Mobile Country Code (MCC): Germany (Federal Republic of) (262)
    Add Number as tbcd  ${mnc}  # Mobile Network Code (MNC): Dolphin Telecom (Deutschland) GmbH (12)

Add RAT Type
    Add Decimal as Octets  82  1  # IE Type: RAT Type (82)
    Add Decimal as Octets  1  2  # IE Length: 1
    Add Decimal as Octets  0  1  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    Add Decimal as Octets  6  1  # RAT Type: EUTRAN (6)

Add Indication
    Add Decimal as Octets  77  1  # IE Type: Indication (77)
    Add Decimal as Octets  2  2  # IE Length: 2
    Add Decimal as Octets  0  1  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    Add Decimal as Octets  0  1  # 0... .... = DAF (Dual Address Bearer Flag): False, .0.. .... = DTF (Direct Tunnel Flag): False, ..0. .... = HI (Handover Indication): False, ...0 .... = DFI (Direct Forwarding Indication): False, .... 0... = OI (Operation Indication): False, .... .0.. = ISRSI (Idle mode Signalling Reduction Supported Indication): False, .... ..0. = ISRAI (Idle mode Signalling Reduction Activation Indication): False, .... ...0 = SGWCI (SGW Change Indication): False
    Add Decimal as Octets  0  1  # .... 0... = PT (Protocol Type): False, .... .0.. = TDI (Teardown Indication): False, .... ..0. = SI (Scope Indication): False, .... ...0 = MSV (MS Validated): False

Add Fully Qualified Tunnel Endpoint Identifier (F-TEID)
    [Arguments]  ${teidgre}  ${ip}
    Add Decimal as Octets  87  1  # IE Type: Fully Qualified Tunnel Endpoint Identifier (F-TEID) (87)
    Add Decimal as Octets  9  2  # IE Length: 9
    Add Decimal as Octets  0  1  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    Add Decimal as Octets  138  1  # 1... .... = V4 (True-IPV4 address field Exists,False-Doesn't Exist in F-TEID): True, .0.. .... = V6 (True-IPV6 address field Exists,False-Doesn't Exist in F-TEID): False, ...0 1010 = Interface Type: S11 MME GTP-C interface (10)
    Add Decimal as Octets  ${teidgre}  4  # TEID/GRE Key: 50331665
    Add Ip As Hex  ${ip}  # F-TEID IPv4: 192.168.112.10 (192.168.112.10)

Add Access Point Name (APN)
    Add Decimal as Octets  71  1  # IE Type: Access Point Name (APN) (71)
    Add Decimal as Octets  31  2  # IE Length: 31
    Add Decimal as Octets  0  1  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    Add String  sgw.nsn.com.mnc012.mcc262.gprs  31  # APN (Access Point Name): sgw.nsn.com.mnc012.mcc262.gprs

Add Selection Mode
    Add Decimal as Octets  128  1  # IE Type: Selection Mode (128)
    Add Decimal as Octets  1  2  # IE Length: 1
    Add Decimal as Octets  0  1  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    Add Decimal as Octets  252  1  # .... ..00 = Selection Mode: MS or network provided APN, subscribed verified (0)

Add PDN Type
    Add Decimal as Octets  99  1  # IE Type: PDN Type (99)
    Add Decimal as Octets  1  2  # IE Length: 1
    Add Decimal as Octets  0  1  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    Add Decimal as Octets  1  1  # .... .001 = PDN Type: IPv4 (1)

Add PDN Address Allocation (PAA)
    Add Decimal as Octets  79  1  # IE Type: PDN Address Allocation (PAA) (79)
    Add Decimal as Octets  5  2  # IE Length: 5
    Add Decimal as Octets  0  1  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    Add Decimal as Octets  1  1  # .... .001 = PDN Type: IPv4 (1)
    Add Ip As Hex  0.0.0.0  # PDN IPv4: 0.0.0.0 (0.0.0.0)

Add APN Restriction
    Add Decimal as Octets  127  1  # IE Type: APN Restriction (127)
    Add Decimal as Octets  1  2  # IE Length: 1
    Add Decimal as Octets  0  1  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    Add Decimal as Octets  0  1  # APN Restriction: 0

Add Aggregate Maximum Bit Rate (AMBR)
    Add Decimal as Octets  72  1  # IE Type: Aggregate Maximum Bit Rate (AMBR) (72)
    Add Decimal as Octets  8  2  # IE Length: 8
    Add Decimal as Octets  0  1  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    Add Decimal as Octets  2  4  # AMBR Uplink (Aggregate Maximum Bit Rate for Uplink): 2
    Add Decimal as Octets  1  4  # AMBR Downlink(Aggregate Maximum Bit Rate for Downlink): 1

Add Bearer Context : [Grouped IE]
    Add Decimal as Octets  93  1  # IE Type: Bearer Context (93)
    Add Decimal as Octets  31  2  # IE Length: 31
    Add Decimal as Octets  0  1  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    Add EPS Bearer ID (EBI)
    Add Bearer Level Quality of Service (Bearer QoS)

Add EPS Bearer ID (EBI)
    Add Decimal as Octets  73  1  # IE Type: EPS Bearer ID (EBI) (73)
    Add Decimal as Octets  1  2  # IE Length: 1
    Add Decimal as Octets  0  1  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    Add Decimal as Octets  5  1  # .... 0101 = EPS Bearer ID (EBI): 5

Add Bearer Level Quality of Service (Bearer QoS)
    Add Decimal as Octets  80  1  # IE Type: Bearer Level Quality of Service (Bearer QoS) (80)
    Add Decimal as Octets  22  2  # IE Length: 22
    Add Decimal as Octets  0  1  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    Add Decimal as Octets  4  1  # .... ...0 = PVI (Pre-emption Vulnerability): False, ..00 01.. = PL (Priority Level): 1, .0.. .... = PCI (Pre-emption Capability): False
    Add Decimal as Octets  9  1  # Label (QCI): 9
    Add Decimal as Octets  0  5  # Maximum Bit Rate For Uplink: 0
    Add Decimal as Octets  0  5  # Maximum Bit Rate For Downlink: 0
    Add Decimal as Octets  0  5  # Guaranteed Bit Rate For Uplink: 0
    Add Decimal as Octets  0  5  # Guaranteed Bit Rate For Downlink: 0

Add Recovery (Restart Counter)
    Add Decimal as Octets  3  1  # IE Type: Recovery (Restart Counter) (3)
    Add Decimal as Octets  1  2  # IE Length: 1
    Add Decimal as Octets  0  1  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    Add Decimal as Octets  1  1  # Restart Counter: 1

Add UE Time Zone
    Add Decimal as Octets  114  1  # IE Type: UE Time Zone (114)
    Add Decimal as Octets  2  2  # IE Length: 2
    Add Decimal as Octets  0  1  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    Add Decimal as Octets  8  1  # Time Zone: 8
    Add Decimal as Octets  0  1  # .... ..00 = Daylight Saving Time: No Adjustments for Daylight Saving Time (0)

Add Charging Characteristics
    Add Decimal as Octets  95  1  # IE Type: Charging Characteristics (95)
    Add Decimal as Octets  2  2  # IE Length: 2
    Add Decimal as Octets  0  1  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    Add Decimal as Octets  12800  2  # Charging Characteristic: 12800

Validate ie type And length
    [Arguments]  ${type}  ${length}
    ${ie-type}=  Read From Data  1
    Should Be Equal  ${ie-type}  ${type}
    ${ie-length}=  Read From Data  2
    Should Be Equal  ${ie-length}  ${length}

Validate cr-flags and instance
    [Arguments]  ${value}
    ${cr-flags-and-instance}=  Read From Data  1
    Should Be Equal  ${cr-flags-and-instance}  ${value}  # 000. .... = CR flag: 0, .... 0000 = Instance: 0

Validate mcc and mnc
    [Arguments]  ${mcc}  ${mnc}
    read tbcd coded numbers from data  3
    ${mcc-act}  read from tbcd  3
    read tbcd coded numbers from data  2
    Should be equal  ${mcc}  ${mcc-act}  # Mobile Country Code (MCC): Germany (Federal Republic of) (262)
    ${mnc-act}  read from tbcd  2
    Should Be Equal  ${mnc}  ${mnc-act}  # Mobile Network Code (MNC): Dolphin Telecom (Deutschland) GmbH (12)

Validate Flags
    [Arguments]  ${expected}
    ${actual}=  Read From Data  1
    Should Be Equal  ${expected}  ${actual}

Validate Message Type
    [Arguments]  ${expected}
    ${actual}=  Read From Data  1
    Should Be Equal  ${actual}  ${expected}

Validate Message Length
    [Arguments]  ${expected}
    ${actual}=  Read From Data  2
    Should Be Equal  ${expected}  ${actual}

Validate Tunnel Endpoint Identifier
    [Arguments]  ${expected}
    ${actual}=  Read From Data  4
    Should Be Equal  ${actual}  ${expected}

Validate Sequence Number
    [Arguments]  ${expected}
    ${actual}=  Read From Data  3
    Should Be Equal  ${actual}  ${expected}

Validate International Mobile Subscriber Identity (IMSI)
    [Arguments]  ${expected}
    Validate ie type and length  1  8
    Validate cr-flags and instance  0  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    Read TBCD Coded Numbers From Data  15
    ${actual}=  Read From TBCD  15  # IMSI(International Mobile Subscriber Identity number): 262120000000001
    Should Be Equal  ${actual}  ${expected}

Validate MSISDN
    [Arguments]  ${country_code}  ${address_digits}
    Validate ie type And length  76  8
    Validate cr-flags and instance  0  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    read tbcd coded numbers from data  16
    ${cc}=  Read From TBCD  3
    ${ad}=  Read From TBCD  13
    Should Be Equal  ${cc}  ${country_code}
    Should Be Equal  ${ad}  ${address_digits}

Validate User Location Info (ULI)
    [Arguments]  ${mcc}  ${mnc}
    Validate ie type and length  86  13  # IE Type: User Location Info (ULI) (86), length: 13
    Validate cr-flags and instance  0  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    Read Binary From Data  1
    Read From Binary  3  # spare bits
    ${flags}=  Read From Binary  5
    Should Be Equal  ${flags}  24  # ...1 .... = ECGI Present Flag): True, .... 1... = TAI Present Flag): True, .... .0.. = RAI Present Flag): False, .... ..0. = SAI Present Flag): False, .... ...0 = CGI Present Flag): False
    Validate Tracking Area Identity(TAI)  ${mcc}  ${mnc}
    Validate E-UTRAN Cell Global Identifier (ECGI)  ${mcc}  ${mnc}

Validate Tracking Area Identity(TAI)
    [Arguments]  ${mcc}  ${mnc}
    Validate mcc and mnc  ${mcc}  ${mnc}
    ${tac-act}  Read From Data  2
    Should Be Equal  1  ${tac-act}  # Tracking Area Code: 1

Validate E-UTRAN Cell Global Identifier (ECGI)
    [Arguments]  ${mcc}  ${mnc}
    Validate mcc and mnc  ${mcc}  ${mnc}
    ${cid}=  Read From Data  4
    Should Be Equal  ${cid}  234  # Spare: 0, ECI (E-UTRAN Cell Identifier): 234

Validate Serving Network
    [Arguments]  ${mcc}  ${mnc}
    Validate ie type and length  83  3  # IE Type: Serving Network (83) # IE Length: 3
    Validate cr-flags and instance  0  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    Validate mcc and mnc  ${mcc}  ${mnc}

Validate RAT Type
    Validate ie type and length  82  1  #RAT Type (82)
    Validate cr-flags and instance  0  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    ${rat-type}=  Read From Data  1
    Should Be Equal  ${rat-type}  6  #EUTRAN (6)

Validate Charging Characteristic
    Validate ie type and length  95  2
    Validate cr-flags and instance  0
    ${charging-characteristic}=  Read From Data  2
    Should Be Equal  ${charging-characteristic}  12800

Validate Indication
    Validate ie type and length  77  2  # IE Type: Indication (77)
    Validate cr-flags and instance  0  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    ${flags1}=  Read From Data  1
    Should Be Equal  0  ${flags1}  # 0... .... = DAF (Dual Address Bearer Flag): False, .0.. .... = DTF (Direct Tunnel Flag): False, ..0. .... = HI (Handover Indication): False, ...0 .... = DFI (Direct Forwarding Indication): False, .... 0... = OI (Operation Indication): False, .... .0.. = ISRSI (Idle mode Signalling Reduction Supported Indication): False, .... ..0. = ISRAI (Idle mode Signalling Reduction Activation Indication): False, .... ...0 = SGWCI (SGW Change Indication): False
    ${flags2}=  Read From Data  1
    Should Be Equal  0  ${flags2}  # .... 0... = PT (Protocol Type): False, .... .0.. = TDI (Teardown Indication): False, .... ..0. = SI (Scope Indication): False, .... ...0 = MSV (MS Validated): False

Validate Fully Qualified Tunnel Endpoint Identifier (F-TEID)
    [Arguments]  ${teidgre}  ${ip}
    Validate ie type and length  87  9  # IE Type: Fully Qualified Tunnel Endpoint Identifier (F-TEID) (87) # IE Length: 9
    Validate cr-flags and instance  0
    ${flags}=  Read From Data  1
    Should Be Equal  138  ${flags}  # 1... .... = V4 (True-IPV4 address field Exists,False-Doesn't Exist in F-TEID): True, .0.. .... = V6 (True-IPV6 address field Exists,False-Doesn't Exist in F-TEID): False, ...0 1010 = Interface Type: S11 MME GTP-C interface (10)
    ${teidgre-act}=  Read From Data  4
    Should Be Equal  ${teidgre}  ${teidgre-act}  # TEID/GRE Key: 50331665
    ${ip-act}=  Read Ip From Hex
    Should Be Equal  ${ip}  ${ip-act}  # F-TEID IPv4: 192.168.112.10 (192.168.112.10)

Validate Access Point Name (APN)
    Validate ie type and length  71  31  # IE Type: Access Point Name (APN) (71)
    Validate cr-flags and instance  0  # 000. .... = CR flag: 0, .... 0000 = Instance: 0
    ${string}=  Read From Data  31
    Should Be Equal  sgw.nsn.com.mnc012.mcc262.gprs  ${string}  # APN (Access Point Name): sgw.nsn.com.mnc012.mcc262.gprs

