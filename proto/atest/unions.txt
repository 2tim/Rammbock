*** Settings ***
Test Setup        Setup protocol, UDP server, and client
Test teardown     Teardown rammbock and increment port numbers
Library           Rammbock.py
Library           message_tools.py
Resource          Protocols.txt
Default Tags      Regression


*** Test Cases ***
Simple union
    Client sends simple union request     simpleU:fooMiddle   simpleU.fooMiddle:0xf00d
    ${msg} =    Server receives simple union request
    Should be equal    ${msg.simpleU.fooBig.hex}        0xf00d0000
    Should be equal    ${msg.simpleU.fooMiddle.hex}     0xf00d
    Should be equal    ${msg.simpleU.fooShort.hex}      0xf0

Complex union
    Client sends complex union request     complexU:complexType  complexU.complexType.first:1    complexU.complexType.second:2
    ${msg} =    Server receives complex union request
    Should be equal as integers    ${msg.complexU.complexType.first.int}     1
    Should be equal as integers    ${msg.complexU.complexType.second.int}     2
    Should be equal as integers    ${msg.complexU.basicType.int}     0

Hyper complex union
    Client sends hyper complex union request    hyperU:user
    ${msg} =    Server receives hyper complex union request
    Should be equal    ${msg.hyperU.user.name.ascii}     johnsson

Choosing union when receiving
    [Tags]
    Client sends hyper complex union request    hyperU:machine
    ${msg} =    Server receives hyper complex union request    hyperU:machine
    Field should exist        ${msg.hyperU}     machine 
    Field should not exist    ${msg.hyperU}     user


*** Keywords ***
Client sends simple union request
    [Arguments]    @{params}
    Simple union message
    Client sends message   @{params}

Server receives simple union request
    [Arguments]    @{params}
    Simple union message
    ${msg} =    Server Receives message    @{params}
    [return]    ${msg}

Client sends complex union request
    [Arguments]    @{params}
    Complex union message
    Client sends message   @{params}

Server receives complex union request
    [Arguments]    @{params}
    Complex union message
    ${msg} =    Server Receives message    @{params}
    [return]    ${msg}

Client sends hyper complex union request
    [Arguments]    @{params}
    Hyper complex union message
    value   hyperU.user.name    johnsson
    value   hyperU.user.age    47
    value   hyperU.machine.id    1337
    value   hyperU.machine.price    9500
    Client sends message   @{params}

Server receives hyper complex union request
    [Arguments]    @{params}
    Hyper complex union message
    ${msg} =    Server Receives message    @{params}
    [return]    ${msg}

Simple union message
   New message    UnionMessage   Example  messageType:0xb00b
   SimpleUnion   simpleU

Complex union message
   New message   ComplexUnion    Example  messageType:0xb00b
   ComplexUnion  complexU

Simple union
   [arguments]    ${name}
   Union          Simple   ${name}   
   u32            fooBig
   u16            fooMiddle
   u8             fooShort
   End union

Complex union
   [arguments]   ${name}
   Union         Complex   ${name}
   u8            basicType
   Tuple         complexType
   End union

Tuple
    [arguments]     ${name}
    Struct    Tuple    ${name}
    u16    first
    u16    second
    End struct

Hyper Complex union message
   New message   HyperUnion    Example  messageType:0xb00b
   HyperUnion  hyperU

HyperUnion
   [arguments]   ${name}
   Union         Hyper   ${name}
   Machine       machine
   User          user
   End union

Machine
    [arguments]     ${name}
    Struct    Machine    ${name}
    u32       id
    u16       price
    End struct

User
    [arguments]     ${name}
    Struct    User    ${name}
    char   10       name
    u32            age
    End struct
