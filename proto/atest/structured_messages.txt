*** Settings ***
Test Setup        Setup protocol, UDP server, and client
Test teardown     Teardown rammbock and increment port numbers
Library           Rammbock.py
Resource          Protocols.txt

*** Variables ***
${HEADER} =    0x0100aaaa0e0000

*** Test Cases ***
Message with struct
    [Tags]    Regression
    Client sends request with struct     pair.first:0xba   pair.second:0xbe
    ${msg} =    Server receives request with struct
    Should be equal    ${msg.pair.first.hex}     0xba
    Should be equal    ${msg.pair.second.hex}    0xbe

Validate with struct
    [Tags]    Regression
    Client sends request with struct     pair.first:0xba   pair.second:0xbe
    Server receives request with struct    pair.first:0xba

Fail validation with struct
    [Tags]    Regression
    Client sends request with struct     pair.first:0xba   pair.second:0xbe
    Server should fail validation with struct    pair.first:0xff

Message with list
    [Tags]    Regression
    Client sends request with list
    ${msg} =    Server receives request with list    someList[2]:3
    Should be equal as integers    ${msg.someList[4].int}     3

Message with list validation fails
    [Tags]    Regression
    Client sends request with list
    Server should fail validation with list   someList[2]:4

Dynamic message
    Client Sends hex    ${HEADER} 03 cafe babe d00d
    ${msg} =    Server Receives dynamic request
    Should be equal    ${msg.pairList[0].first.hex}     0xca
    Should be equal    ${msg.pairList[0].second.hex}    0xfe
    Should be equal    ${msg.pairList[2].first.hex}     0xd0

Struct containing struct
    Client sends struct with struct   structstruct.mollyMalones.closesAt:4
    ${msg} =    Server Receives struct with struct  structstruct.mollyMalones.closesAt:
    Should be equal    ${msg.structstruct.mollyMalones.closesAt.int}   4


*** Keywords ***
Client sends request with struct
    [Arguments]    @{params}
    Message with struct
    Client sends message   @{params}

Client sends request with list
    [Arguments]    @{params}
    List message
    Client sends message   @{params}

Server receives request with struct
    [Arguments]    @{params}
    Message with struct
    ${msg} =    Server Receives message    @{params}
    [return]    ${msg}

Server should fail validation with struct
    [Arguments]    @{params}
    Run keyword and expect error     Value of field first does not *    Server receives request with struct    @{params}

Server receives request with list
    [Arguments]    @{params}
    List message
    ${msg} =    Server Receives message    @{params}
    [return]    ${msg}

Server should fail validation with list
    [Arguments]    @{params}
    Run keyword and expect error     Value of field *    Server receives request with list    @{params}

Server Receives dynamic request
    [Arguments]    @{params}
    Dynamic message
    ${msg} =    Server Receives message    @{params}
    [return]    ${msg}

List message
    New message    ListRequest    Example    messageType:0xaaaa
    Array   5    u32    someList    3

Dynamic message
    New Message    DynamicRequest  Example    messageType:0xaaaa
    u8      numberOfPairs
    Array   numberOfPairs    Pair    pairList

Message with struct
    New Message    StructRequest  Example    messageType:0xaaaa
    Pair    pair

Array
    [arguments]    ${size}    ${type}    ${name}    ${value}=
    New List       ${size}    ${name}
    Run keyword    ${type}    ${name}    ${value}
    End List

Pair
    [arguments]     ${name}
    Struct    Pair    ${name}
    u8    first
    u8    second
    End struct

Message with struct in struct
    New Message   StructStruct   Example    messageType:0xaaaa
    StructStruct   structstruct

StructStruct
    [Arguments]    ${name}
    Struct   StructStruct   ${name}
    u32      foo    6
    Bar      mollyMalones
    End struct

Bar
    [Arguments]    ${name}
    Struct   Bar   ${name}
    u8        opensAt    10
    u8        closesAt    2
    End struct