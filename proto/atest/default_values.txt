*** Settings ***
Resource          Protocols.txt
Test Setup        Setup protocol, UDP server, and client
Test teardown     Teardown rammbock and increment port numbers

*** Test cases ***
Client send server receive
    Client sends     Value ExampleMessage    faa:5
    Client sends     Value ExampleMessage    faa:5
    ${msg}=     Server receives     Value ExampleMessage
    ${msg}=     Server receives     Value ExampleMessage    
    Should be equal as integers    1    ${msg.foo}
    Should be equal as integers    10   ${msg.bar} 
    Should be equal as integers    5    ${msg.faa}    

Server send client receive
    Send first message to server to identify client   #TODO: Implement send to
    Server sends     Value ExampleMessage    faa:5
    ${msg}=     Client receives    Value ExampleMessage
    Should be equal as integers    1    ${msg.foo}
    Should be equal as integers    10   ${msg.bar} 
    Should be equal as integers    5    ${msg.faa}    
    
*** Keywords ***
ExampleMessage
    New Message     ExampleMessage    Example    messageType:0xbabe  
    u16             foo       1
    u8              bar       2
    u8              faa       
    
Value ExampleMessage
    ExampleMessage
    Value           bar      10
    
Client sends
    [Arguments]    ${message type}   @{params}
    Run keyword    ${message type}
    Client sends message    @{params}

Server sends 
    [Arguments]    ${message type}   @{params}
    Run keyword    ${message type}
    Server sends message    @{params}
    
Server receives
    [Arguments]    ${message type}   @{params}
    Run keyword    ${message type}
    ${msg}=      Server receives message    @{params}  
    [Return]     ${msg}
    
Client receives
    [Arguments]    ${message type}   @{params}
    Run keyword    ${message type}
    ${msg}=      Client receives message    @{params}  
    [Return]     ${msg}
    
Send first message to server to identify client    
    Client sends     Value ExampleMessage    faa:5
    ${msg}=     Server receives     Value ExampleMessage
    
