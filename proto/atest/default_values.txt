*** Settings ***
Resource          Protocols.txt
Test Setup        Setup protocol, UDP server, and client
Test teardown     Teardown rammbock and increment port numbers
Force Tags        Regression

*** Test cases ***
Client sends server receives
    Client sends     Value ExampleMessage    faa:200
    ${msg}=     Server receives     Value ExampleMessage   faa:200
    Verify message fields    ${msg}

Server sends client receives
    Send first message to server to identify client   #TODO: Implement send to
    Server sends     Value ExampleMessage    faa:200
    ${msg}=     Client receives    Value ExampleMessage   faa:200
    Verify message fields    ${msg}
    
    
*** Keywords ***
Verify message fields
    [Arguments]   ${msg}
    Should be equal as integers    1    ${msg.foo.int}
    Should be equal as integers    10   ${msg.bar.int} 
    Should be equal as integers    200    ${msg.faa.int}    
    
ExampleMessage
    New Message     ExampleMessage    Example    messageType:0xbabe  
    u16             foo       1
    u8              bar       2
    u8              faa       
    
Value ExampleMessage
    ExampleMessage
    Value           bar      10
    Value           faa      20
    
Client sends
    [Arguments]    ${message type}   @{params}
    Run keyword    ${message type}
    Client sends message    @{params}

Server sends 
    [Arguments]    ${message type}   @{params}
    Run keyword    ${message type}
    Server sends message    @{params}
    
Server receives
    [Arguments]    ${message type}   @{params}
    Run keyword    ${message type}
    ${msg}=      Server receives message    @{params}  
    [Return]     ${msg}
    
Client receives
    [Arguments]    ${message type}   @{params}
    Run keyword    ${message type}
    ${msg}=      Client receives message    @{params}  
    [Return]     ${msg}
    
Send first message to server to identify client    
    Client sends     Value ExampleMessage
    ${msg}=     Server receives     Value ExampleMessage
    
