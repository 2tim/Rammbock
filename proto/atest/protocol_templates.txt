*** Settings ***
Test Setup        Setup protocol, UDP server, and client
Test teardown     Teardown rammbock and increment port numbers
Resource          Protocols.txt

*** Test Cases ***
Client uses protocol and sends
    Client Sends simple request    value:0xcafebabe
    Verify server gets hex    0x 01 00 dddd 000c ffff cafebabe

Server uses protocol and sends
    Client sends request and server receives it
    Server Sends simple request    value:0xcafebabe
    Verify client gets hex    0x 01 00 dddd 000c ffff cafebabe

Server uses protocol and receives
    Client Sends hex    0x 01 00 dddd 0c 0000 cafebabe
    ${msg} =    Server Receives simple request    value:0xcafebabe
    Should be equal    ${msg.value.hex}    0xcafebabe

Server uses protocol and value validation fails
    Client Sends hex    0x 01 00 dddd 0c 0000 cafebabe
    Run keyword and expect error    Value of field value does not match *    Server Receives simple request    value:0xffffff

Server uses protocol and messages arrive out of order
    Client Sends hex    0x 01 00 aaaa 0c 0000 deadbeef
    Client Sends hex    0x 01 00 dddd 0c 0000 cafebabe
    ${msg dddd} =    Server Receives simple request
    Should be equal    ${msg dddd._header.messageType.hex}    0xdddd

Message field type conversions
    Client Sends hex    0x 01 00 dddd 0c 0000 000000ff
    ${msg} =    Server Receives simple request    value:0xcafebabe
    Should be equal    ${msg.value.hex}    0x000000ff
    Should be equal as integers    ${msg.value.int}    255
    Should be equal    ${msg.value.bytes}    \x00\x00\x00\xff

Define And Use A Message Template
    [Tags]    regression
    [setup]  Define Simple Protocol
    Defined Protocol is usable in client

Undefined protocol cannot be used
    [Tags]    regression
    [setup]  Define Simple Protocol
    Using undefined protocol in client fails

Access header
    Client Sends hex    0x 01 00 dddd 0c 0000 000000ff
    ${msg} =    Server Receives simple request
    Should be equal    ${msg._header.version.hex}    0x01
    Should be equal as integers    ${msg._header.length.int}    12

*** Keywords ***
Client Sends simple request
    [Arguments]    @{params}
    New message    ValueRequest  Example    messageType:0xdddd    flags:0xffff
    u32    value    0xdeadbeef
    Client Sends message    @{params}

Server Sends simple request
    [Arguments]    @{params}
    New message    ValueRequest  Example    messageType:0xdddd    flags:0xffff
    u32    value    0xdeadbeef
    Server Sends message    @{params}

Server Receives simple request
    [Arguments]    @{params}
    New message    ValueRequest   Example    messageType:0xdddd    #messageType:(0xdd|0xff)
    u32    value
    ${msg} =    Server receives message    @{params}

Client sends request and server receives it
    Client Sends simple request
    Verify server gets hex    0x 01 00 dddd 000c ffff deadbeef

u32
    [arguments]     ${name}   ${default_value}=""
    uint  4   ${name}   ${default_value}