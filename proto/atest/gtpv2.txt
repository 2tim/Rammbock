*** Settings ***
Test Setup        Create server and client, and define protocol
Test Teardown     Reset Rammbock
Default Tags      regression
Resource          Protocols.txt

*** Variables ***
${GTP CONTROL PORT}    2123
${LOCALHOST IP}    127.0.0.1
${CREATE SESSION REQUEST}    32

*** Test Cases ***
Create Session Request
    Create and send Create Session Request
    Receive and Validate Create Session Request

*** Keywords ***
Create and send Create Session Request
    Create Session Request
    client sends message   header:sequence number:48

Receive and Validate Create Session Request
    Create Session Request
    ${msg}=    Server Receives message    header:sequence number:48

Create server and client, and define protocol
    define gtpv2 protocol
    start udp server    ${LOCALHOST IP}    ${GTP CONTROL PORT}    protocol=gtpv2
    start udp client    ip=${LOCALHOST IP}    protocol=gtpv2
    connect    ${LOCALHOST IP}    ${GTP CONTROL PORT}

define gtpv2 protocol
    new protocol    gtpv2
    u8    flags    72
    u8    message type
    u16   message length
    u32   tunnel endpoint identifier    0
    u24   sequence number
    u8    spare    0
    pdu    message length-12
    end protocol

Create Session Request
    new message    create session request    gtpv2    message type:${CREATE SESSION REQUEST}
    IMSI       instance.value:0x01    value.imsi.value:262120000000001
    MSISDN
    ULI    value.flags:0b00011000    value.tai.mcc.value:262  value.tai.mnc.value:12       value.ecgi.mcc.value:262    value.ecgi.mnc.value:12    value.ecgi.eci.ecgi:234    value.tai.tracking_area_code:1
    Serving network   value.mcc.value:262    value.mnc.value:12
    Rat type    value.rat_type:6
    Indication    value.fields.DAF:1
    F-TEID    1    value.f-teid_ipv4:0xc0a8700a    value.values.interface type:7
    F-TEID    2    instance.value:1    value.f-teid_ipv4:0x0a66b6b8    value.values.interface type:10
    APN     value.access_point_name:sgw.foo.com.mnc012.mcc262.gprs
    Selection mode
    PDN Type    value.pdn_type.value:1
    PDN Address Allocation    value.pdn_type.value:1    value.pdn_address_and_prefix:0x00
    APN Restriction    value.apn_restriction:0
    Aggregate maximum bit rate    value.ambr_uplink:2    value.ambr_downlink:1
    bearer context    value.eps_bearer_id.value.epsbid.value:5    value.bearer_level_qos.value.arp.pl:1   value.bearer_level_qos.value.label:9
    recovery   value.recovery:1
    ue time zone   value.timezone.value:0
    charging characteristics

charging characteristics fields
    u16    charging_characteristic    0x3200

charging characteristics
    [arguments]     @{args}
    IE    charging characteristics    95    charging characteristics fields    @{args}

ue time zone fields
    new binary container    timezone
    bin   6    spare   0b111111
    bin   2    value
    end binary container
    new binary container    dst
    bin   6    spare   0
    bin   2    value   0
    end binary container

ue time zone
    [arguments]    @{args}
    IE    ue time zone    114    ue time zone fields    @{args}

recovery fields
    u8    recovery

recovery
    [arguments]    @{args}
    IE    Recovery    3    recovery fields    @{args}

EPS bearer id fields
    new binary container    epsbid
    bin    4    spare   0
    bin    4    value
    end binary container

Bearer level quality of service fields
    new binary container   arp
    bin   1    spare   0
    bin   1    pci    0
    bin   4    pl
    bin   1    spare_2   0
    bin   1    pvi     0
    end binary container
    u8    label
    u40    mbr_uplink      0
    u40    mbr_downlink    0
    u40    gbr_uplink      0
    u40    gbr_downlink    0

bearer context fields
    IE       eps_bearer_id    73    EPS bearer id fields
    IE       bearer_level_qos    80    Bearer level quality of service fields

bearer context
    [arguments]   @{args}
    IE     bearer context    93    bearer context fields    @{args}

Aggregate maximum bit rate fields
    u32    ambr_uplink
    u32    ambr_downlink

Aggregate maximum bit rate
    [arguments]    @{args}
    IE    Aggregate maximum bit rate    72    Aggregate maximum bit rate fields    @{args}

APN Restriction fields
    u8    apn_restriction

APN Restriction
    [arguments]    @{args}
    IE    APN Restriction      127    APN Restriction fields    @{args}

PDN Address Allocation
    [arguments]    @{args}
    IE    PDN Address Allocation    79    PDN Address Allocation fields    @{args}

PDN Address Allocation fields
    new Binary container    pdn_type
    bin    5    spare    0
    bin    3    value
    end binary container
    u32    pdn_address_and_prefix

PDN Type
    [arguments]    @{args}
    IE    PDN Type    99    PDN Type field    @{args}

PDN Type field
    new Binary container    pdn_type
    bin    5    spare    0
    bin    3    value
    end binary container

Selection mode
    [arguments]    @{args}
    IE    Selection mode    128    Selection mode field    @{args}

Selection mode field
    new Binary container    selection_mode
    bin    6    spare    0b111111
    bin    2    value    0
    end binary container

APN
    [arguments]    @{args}
    IE    APN    71    APN Field    @{args}

apn field
    chars  *  access_point_name

F-TEID
    [Arguments]   ${number}    @{args}
    IE    F-TEID${number}    87    F-TEID fields    @{args}

F-TEID fields
    [Arguments]    @{args}
    new binary container    values
    bin    1    v4    1
    bin    1    v6    0
    bin    6    interface type
    end binary container
    u32    teid_gre_key    0x00
    u32    f-teid_ipv4


Indication
    [Arguments]    @{args}
    IE    indication    77    indication fields    @{args}

indication fields
    new binary container    fields
    Bin    1    DAF     0
    Bin    1    DTF     0
    Bin    1    HI      0
    Bin    1    DFI     0
    Bin    1    OI      0
    Bin    1    ISRSI   0
    Bin    1    ISRAI   0
    Bin    1    SGWCI   0
    Bin    1    SQCI    0
    Bin    1    UIMSI   0
    Bin    1    CFSI    0
    Bin    1    CRSI    0
    Bin    1    PS      0
    Bin    1    PT      0
    Bin    1    SI      0
    Bin    1    MSV     0
    Bin    3    SPARE   0
    Bin    1    S6AF    0
    Bin    1    S4AF    0
    Bin    1    MBMDT   0
    Bin    1    ISRAU   0
    Bin    1    CCRSI   0
    end binary container

rat type
    [arguments]    @{args}
    IE    rat type    82    rat type value    @{args}

rat type value
    u8    rat_type

Serving network
    [Arguments]    @{values}
    IE    serving network   83    serving network fields    @{values}

serving network fields
    [Arguments]    @{arguments}
    new tbcd container    mcc
    tbcd    3    value
    end tbcd container
    new tbcd container    mnc
    tbcd    2    value
    end tbcd container

ULI
    [Arguments]    @{values}
    IE    uli    86    uli fields    @{values}

UlI fields
    [Arguments]       @{args}
    u8    flags
    TAI
    ECGI

ECGI
    new struct    E-UTRAN Cell Global Identifier    ecgi
    single value tbcd container    mcc    3        ${none}
    single value tbcd container    mnc    2        ${none}
    new binary container   eci
    Bin    4    spare    0
    bin    28   ecgi
    end binary container
    end struct

TAI
    new struct    tai    tai
    single value tbcd container    mcc    3        ${none}
    single value tbcd container    mnc    2        ${none}
    u16    tracking_area_code
    end struct

IMSI
    [Arguments]    @{args}
    IE    imsi    1    imsi fields  @{args}

imsi fields
    new tbcd container    imsi
    tbcd    15    value
    end tbcd container

MSISDN
    [Arguments]    @{args}
    IE    msisdn    76    msisdn fields    @{args}

msisdn fields
    new tbcd container    msisdn
    tbcd    3    country_code       358
    tbcd    *   address_digits      6100000000001
    end tbcd container

single value tbcd container
    [Arguments]    ${name}    ${len}    ${value}
    new tbcd container    ${name}
    tbcd   ${len}    value    ${value}
    end tbcd container

IE
    [Arguments]    ${name}    ${type}    ${content}    @{values}
    new Struct    IE    ${name}    @{values}
    IE header   ${name}    ${type}    ${content}
    IE value    ${content}
    End struct

IE header
    [Arguments]    ${name}    ${type}    ${content}
    u8    ie type    ${type}
    u16    ie_length
    new binary container    instance
    bin    4    spare    0
    bin    4    value    0
    end binary container

IE Value
    [Arguments]    ${kw}    ${name}=value
    new struct    Container    ${name}    length=ie_length
    run keyword    ${kw}
    end struct

ADDRESS
    [Arguments]    ${value}
    IE    address    54    Chars    ie length    ${value}


