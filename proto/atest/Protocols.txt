*** Keywords ***
Define example protocol
    Start Protocol Description    Example
    u8    version    0x01
    u8    reserved    0x00
    u16    messageType    # Empty on purpose, each message template defines the type for it
    u16    length         # Empty on purpose, the length is pdu length
    u16    flags    0x0000
    pdu    length-8
    End protocol description

Setup TCP server and client  [arguments]   ${protocol}=
    Start TCP server    ${SERVER}    ${SERVER PORT}    _name=ExampleServer   _protocol=${protocol}
    Start TCP client    _name=ExampleClient
    Client Connects    ${SERVER}    ${SERVER PORT}    _name=ExampleClient
    Server accepts connection  _name=ExampleServer

Setup UDP server and client  [arguments]  ${protocol}=
    Start udp server    ${SERVER}    ${SERVER PORT}    _name=ExampleServer  _protocol=${protocol}
    Start udp client    _name=ExampleClient     _protocol=Example
    Client Connects    ${SERVER}    ${SERVER PORT}    _name=ExampleClient
    
Setup protocol, UDP server, and client
    Define example protocol
    Setup UDP server and client
    
Start two udp clients
    Start udp client    _ip=${CLIENT}    _port=${CLIENT 1 PORT}    _name=Client_1     _protocol=Example
    Start udp client    _ip=${CLIENT}    _port=${CLIENT 2 PORT}    _name=Client_2     _protocol=Example

Connect two clients
    [Arguments]    ${server port 1}     ${server port 2}
    Client Connects    ${SERVER}    ${server port 1}    _name=Client_1
    Client Connects    ${SERVER}    ${server port 2}    _name=Client_2

Verify server gets hex
    [Arguments]    ${expected hex}
    ${msg} =    Server receives binary
    Binary should equal hex    ${msg}    ${expected hex}

Verify server gets from
    [Arguments]    ${ip}  ${port}  ${expected}   ${server}
    ${from ip}   ${from port}   ${msg} =    Server receives binary from     ${server}
    Should be equal    ${msg}    ${expected}
    Should be equal    ${from ip}    ${ip}
    Should be equal    ${from port}  ${port}

Binary should equal hex
    [arguments]      ${binary}    ${expected hex}
    ${binary in hex} =    bin to hex    ${binary}
    ${expected normalized} =   Normalize hex   ${expected hex}
    Should be equal    ${binary in hex}    ${expected normalized}

Normalize hex
    [Arguments]    ${hex}
    ${bin} =     Hex to bin    ${hex}
    ${normalized}   Bin to hex    ${bin}
    [return]       ${normalized}

Client Sends hex
    [Arguments]    ${hex}    @{params}
    ${binary} =    Hex to bin    ${hex}
    Client Sends binary    ${binary}    @{params}

U8
    [Arguments]    ${name}    ${value}=${NONE}
    uint     1     ${name}    ${value}

U16
    [Arguments]    ${name}    ${value}=${NONE}
    uint     2     ${name}    ${value}