*** Settings ***
Library     Rammbock
Variables   ports.py


*** Variables ***
${TEST MACHINE}   127.0.0.1
${SERVER}         ${TEST MACHINE}
${CLIENT}         ${TEST MACHINE}


*** Keywords ***
Define example protocol
    Start Protocol Description    Example
    u8    version    0x01
    u8    reserved    0x00
    u16    messageType    # Empty on purpose, each message template defines the type for it
    u16    length         # Empty on purpose, the length is pdu length
    u16    flags    0x0000
    pdu    length-8
    End protocol description

Define simple protocol
    Start Protocol Description   Example
    uint  1  field  16
    End Protocol Description

Defined Protocol is usable in client
    Start TCP Client  _protocol=Example

Using undefined protocol in client fails
    Run keyword and expect error  *  Start TCP Client  _protocol=Invalid

Setup TCP server and client  [arguments]   ${protocol}=
    Start TCP server    ${SERVER}    ${SERVER PORT}    _name=ExampleServer   _protocol=${protocol}
    Start TCP client    _name=ExampleClient
    connect    ${SERVER}    ${SERVER PORT}    _name=ExampleClient
    Accept connection  _name=ExampleServer

Setup UDP server and client  [arguments]  ${protocol}=
    Start udp server    ${SERVER}    ${SERVER PORT}    _name=ExampleServer  _protocol=${protocol}
    Start udp client    _name=ExampleClient     _protocol=Example
    connect    ${SERVER}    ${SERVER PORT}    _name=ExampleClient
    
Setup protocol, UDP server, and client
    Define example protocol
    Setup UDP server and client    protocol=Example
    
Start two udp clients
    Start udp client    ${CLIENT}    ${CLIENT 1 PORT}    Client_1
    Start udp client    ${CLIENT}    ${CLIENT 2 PORT}    Client_2

Start two tcp clients
    Start tcp client    ${CLIENT}    ${CLIENT 1 PORT}    Client_1
    Start tcp client    ${CLIENT}    ${CLIENT 2 PORT}    Client_2

Connect two clients
    [Arguments]    ${server port 1}     ${server port 2}
    connect    ${SERVER}    ${server port 1}    Client_1
    connect    ${SERVER}    ${server port 2}    Client_2

Connect two clients and accept connections
    [Arguments]    ${server 1}=    ${server port 1}=${SERVER PORT}     ${server 2}=    ${server port 2}=${SERVER PORT}
    Connect    ${SERVER}    ${server port 1}    _name=Client_1
    Accept connection      _name=${server 1}    _alias=Connection_1
    Connect    ${SERVER}    ${server port 2}    _name=Client_2
    Accept connection      _name=${server 2}    _alias=Connection_2

Two clients send foo and bar
    Client Sends binary    foo   _name=Client_1
    Client Sends binary    bar   _name=Client_2

Verify server gets hex
    [Arguments]    ${expected hex}
    ${msg} =    Server receives binary
    Binary should equal hex    ${msg}    ${expected hex}

Verify client gets hex
    [Arguments]    ${expected hex}
    ${msg} =    Client receives binary
    Binary should equal hex    ${msg}    ${expected hex}

Server '${server}' should get '${msg}' from '${ip}':'${port}'
    Verify server gets from    ${ip}    ${port}    ${msg}    ${server}

'${connection}' on '${server}' should get '${msg}' from '${ip}':'${port}'
    Verify server gets from    ${ip}    ${port}    ${msg}    ${server}   ${connection}

Verify server gets from
    [Arguments]    ${ip}  ${port}  ${expected}   ${server}   ${connection}=
    ${msg}   ${from ip}   ${from port} =    Server receives binary from     _name=${server}   _connection=${connection}
    Should be equal    ${msg}    ${expected}
    Should be equal    ${from ip}    ${ip}
    Should be equal as integers    ${from port}  ${port}

Binary should equal hex
    [arguments]      ${binary}    ${expected hex}
    ${binary in hex} =    bin to hex    ${binary}
    ${expected normalized} =   Normalize hex   ${expected hex}
    Should be equal    ${binary in hex}    ${expected normalized}

Normalize hex
    [Arguments]    ${hex}
    ${bin} =     Hex to bin    ${hex}
    ${normalized}   Bin to hex    ${bin}
    [return]       ${normalized}

Client Sends hex
    [Arguments]    ${hex}    @{params}
    ${binary} =    Hex to bin    ${hex}
    Client Sends binary    ${binary}    @{params}

U8
    [Arguments]    ${name}    ${value}=${NONE}
    uint     1     ${name}    ${value}

U16
    [Arguments]    ${name}    ${value}=${NONE}
    uint     2     ${name}    ${value}

Teardown rammbock and increment port numbers
    Reset Rammbock
    Increment ports

Increment ports
    Increment port    SERVER PORT
    Increment port    SERVER PORT 2
    Increment port    CLIENT 1 PORT
    Increment port    CLIENT 2 PORT

Increment port   [Arguments]   ${name}
    ${new} =    Evaluate   ${${name}}+2
    Set suite variable    ${${name}}    ${new}
