*** Settings ***
Test Setup        Setup protocol, UDP server, and client
Test Teardown     Teardown rammbock and increment port numbers
Default Tags      regression
Resource          template_resources.txt

*** Test Cases ***
Define And Use A Message Template
    [Setup]    Define Simple Protocol
    Start TCP Client  protocol=Example

Undefined protocol cannot be used
    [Setup]    Define Simple Protocol
    Should fail    Start TCP Client    protocol=Invalid

Access header
    Client Sends hex    0x 01 00 dddd 000c 0000 000000ff
    ${msg} =    Server Receives simple request
    Should be equal    ${msg._header.version.hex}    0x01
    Should be equal as integers    ${msg._header.length.int}    12

Starting new protocol in middle of old protocol definition fails
    start protocol description    foo
    Run keyword and expect error    *    start protocol description    bar

Redifining protocol fails
    start protocol description    foo
    uint    1    length
    pdu    length
    end protocol description
    run keyword and expect error    *    start protocol description    foo

Defining message while defining protocol fails
    start protocol description    foo
    run keyword and expect error    *    new message    foo

Overriding header parameters in send
    Client sends simple request    header:flags:0xcafe
    ${msg}=    Server receives simple request
    Should be equal    0xcafe    ${msg._header.flags.hex}

Getting protocol name from client
    ${protocol}     get client protocol
    Should be equal    ${protocol}     Example

Protocol with fixed pdu length
    [Setup]         Define simple protocol
    New Message     Ploo    Example
    u16  first     0xcafe
    u16  second    0xd00d
    ${msg}   Get message
    Should be equal    ${msg.first.hex}   0xcafe

Protocol without PDU fails
    Start Protocol Description   Failing
    u8  field  16
    Should fail   End Protocol Description

Protocol with fields after PDU fail
    Start Protocol Description   Failing
    u8  field  16
    pdu   1
    Should fail  u8   second_field   16

Protocol with several PDU fields fails
    Start Protocol Description   Failing
    u8  field  16
    pdu   1
    Should fail   pdu    1
