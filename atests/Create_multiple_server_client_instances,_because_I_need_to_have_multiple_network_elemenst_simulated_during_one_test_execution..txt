*** Settings ***
Test Setup      Create Two TCP Servers And Clients
Test Teardown   Delete Two Servers And Clients And Verify Deletion
Default Tags    regression
Resource        resources/data_transfer.txt
Resource        resources/server_client.txt

*** Variables ***
${LOCALHOST_IP}  127.0.0.1
${SERVER_NAME1}  testiserveri1
${SERVER_NAME2}  testiserveri2
${DEFAULT_PORT1}  3011
${DEFAULT_PORT2}  23205
${CLIENT_NAME1}  testiclient1
${CLIENT_NAME2}  testiclient2

*** Test Cases ***
Create and delete two instances of Servers
    Server Should Be Running  ${SERVER_NAME1}
    Server Should Be Running  ${SERVER_NAME2}

Create and delete two instances of Clients
    Client Should Be Running  ${CLIENT_NAME1}
    Client Should Be Running  ${CLIENT_NAME2}

Send data between multiple Client and Server instances
    Connect to TCP server  ${HOST}  ${DEFAULT_PORT1}  ${CLIENT_NAME1}
    accept tcp connection  ${SERVER_NAME1}
    Connect to TCP server  ${HOST}  ${DEFAULT_PORT2}  ${CLIENT_NAME2}
    accept tcp connection  ${SERVER_NAME2}
    Client Transfers Data  ${CLIENT_NAME1}
    Server Receives packet  ${SERVER_NAME1}
    Client Transfers Data  ${CLIENT_NAME2}
    Server Receives packet  ${SERVER_NAME2}
    Server Sends packet  ${SERVER_NAME1}
    Client Receives Packet  ${CLIENT_NAME1}
    Server Sends packet  ${SERVER_NAME2}
    Client Receives Packet  ${CLIENT_NAME2}

*** Keywords ***
Create Two TCP Servers And Clients
    Create TCP Server  ${LOCALHOST_IP}  ${DEFAULT_PORT1}  ${SERVER_NAME1}
    Create TCP Server  ${LOCALHOST_IP}  ${DEFAULT_PORT2}  ${SERVER_NAME2}
    Create TCP Client  ${CLIENT_NAME1}
    Create TCP Client  ${CLIENT_NAME2}

Delete Two Servers And Clients And Verify Deletion
    Close Server  ${SERVER_NAME1}
    Run Keyword And Expect Error  *  Server Should Be Running  ${SERVER_NAME1}
    Close Server  ${SERVER_NAME2}
    Run Keyword And Expect Error  *  Server Should Be Running  ${SERVER_NAME2}
    Close Client  ${CLIENT_NAME1}
    Run Keyword And Expect Error  *  Server Should Be Running  ${CLIENT_NAME1}
    Close Client  ${CLIENT_NAME2}
    Run Keyword And Expect Error  *  Server Should Be Running  ${CLIENT_NAME2}

