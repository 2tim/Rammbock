*** Settings ***
Default Tags    regression
Library         ../src/Rammbock.py

*** Variables ***
${expected_string}  ismo_rulettaa
${host}  localhost
${port}  2010
${physical_interface}  lo
${virtual_host}  1.1.1.1

*** Test Cases ***
Send and Receive string over UDP using physical interface
    [Setup]  Initialize UDP Server and Client to physical interface
    Send String Over UDP
    Server Receives String Over UDP
    [Teardown]  Close Connections

Send and Receive string over UDP using virtual interface
    [Tags]
    [Setup]  Initialize UDP Client and Server to virtual interface
    Send String Over UDP
    Server Receives String Over UDP
    [Teardown]  Close Connections and remove virtual interface

*** Keywords ***
Server is started
    [Arguments]  ${interface}
    Start Server  ${interface}  ${port}

Server Receives String Over UDP
    ${rcv_string}=  receive_packet_over_udp
    Should Be Equal  ${rcv_string}  ${expected_string}

Connect to Server
    [Arguments]  ${host}
    Rammbock.connect to server  ${host}  ${port}

Send string over UDP
    send packet over udp  ${expected_string}

Initialize UDP Server and Client to physical interface
    use interface  PHYSICAL  ${physical_interface}
    Server is Started  PHYSICAL
    Connect to Server  ${host}

Close Connections
    Comment  Close Client
    Close Server

Initialize UDP Client and Server to virtual interface
    use interface  VIRTUAL_INTERFACE  ${physical_interface}  1.1.1.1  255.255.255.255  True
    Server is Started  VIRTUAL_INTERFACE
    connect to server  ${virtual_host}

Close Connections and remove virtual interface
    Comment  Close Client
    Close Server
    Delete Interface  VIRTUAL_INTERFACE

