*** Settings ***
Test Setup      TCP Server and Client are Initialized
Test Teardown   Close Connections
Default Tags    regression
Test Timeout    5 seconds
Library         rammbock.Rammbock
Resource        ../message_examples/http/v1_1.txt
Resource        ../resources/server_client.txt

*** Test Cases ***
Send HTTP Get Message And Acquire Response
    [Tags]  no-ci  system-verification
    [Setup]  Create TCP Client And Connect To server  www.nokiasiemensnetworks.com  80
    Client Sends HTTP GET
    Client gets Resp
    [Teardown]  Close Client

Send and receive GET message and verify data
    Client Sends HTTP GET
    Servers Receive and verify data

Send and receive GET message and validate it
    Client Sends HTTP GET
    Server Receives HTTP GET
    Check that there are correct headers
    Check that there are correct Information Elements

Send and Receive Get and Return Response
    [Timeout]  5 seconds
    Clients Sends Get to Server
    Server Sends Response so Client

*** Keywords ***
Servers Receive and verify data
    ${message}=  Server Receives Data
    log  ${message}
    Should Be Equal  ${message}  GET / HTTP/1.1 \r\nHost: www.nokiasiemensnetworks.com\r\nConnection: keep-alive\r\nCache-control: max-age=0\r\nUser-agent: rammbock\r\nAccept: text/html\r\n\r\n

Client gets Resp
    Create Message
    Client Receives Data
    ${request version}=  Read Until  ${SPACE}
    ${response code}=  Read Until  ${SPACE}
    Should Be Equal  ${request version}  HTTP/1.1
    Should Be True  100 <= ${response code} < 600

Client Sends HTTP GET
    Create Message
    Set HTTP GET send
    Client Sends Data

Server Receives HTTP GET
    Create Message
    Server Receives Data
    Set HTTP GET rcv

Check that there are correct headers
    Should Be Equal  ${request method}  GET
    Should Be Equal  ${request uri}  /
    Should Be Equal  ${request version}  HTTP/1.1

Check that there are correct Information Elements
    Should be equal  ${host}  www.nokiasiemensnetworks.com
    Should be equal  ${connection}  keep-alive
    Should be equal  ${cache control}  max-age=0
    Should be equal  ${user agent}  rammbock
    Should be equal  ${accept}  text/html

Clients Sends Get to Server
    Client Sends HTTP GET
    Server Receives HTTP GET
    Check that there are correct headers
    Check that there are correct Information Elements

Server Sends Response so Client
    Server Creates Response and sends it To Client
    Client Receives Response

Server Creates Response and sends it To Client
    Create Message
    Set HTTP Response send
    Server Sends Data

Client Receives Response
    Create Message
    Client Receives HTTP Response
    Verify That Response is Correct

Client Receives HTTP Response
    Create Message
    Client Receives data
    Set HTTP Response rcv

Verify That Response is Correct
    Should Be Equal  ${request version}  HTTP/1.1
    Should Be Equal  ${response code}  200
    Should be equal  ${connection}  keep-alive
    Should be equal  ${content-lenght}  218

