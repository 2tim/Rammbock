*** Settings ***
Test Setup      TCP Server and Client are Initialized
Test Teardown   Close Connections
Default Tags    regression
Resource        resources/data_transfer.txt
Resource        resources/server_client.txt

*** Variables ***
${PORT}  57754

*** Test Cases ***
Send packet from Client to Server
    Single Client Transfers Data
    Single Server Receives packet

Send packet from Client to Server and return it back
    Single Client Transfers Data
    Single Server Receives Packet
    Single Server Sends Packet
    Single Client receives Packet

Send and receive stream over TCP
    Send Packet Stream From Single Client To Single Server
    Send Packet Stream From Single Server To Single Client

Send and receive multiple streams over TCP
    Send Packet Stream From Single Client To Single Server
    Send Packet Stream From Single Server To Single Client
    Send Packet Stream From Single Client To Single Server
    Send Packet Stream From Single Server To Single Client

Server receives connections from two clients
    [Setup]  Server and two clients are initialized
    Connect To TCP Server  localhost  ${PORT}  client1
    Accept TCP Connection  server1  connection1
    Connect To TCP Server  localhost  ${PORT}  client2
    Accept TCP Connection  server1  connection2
    Client sends data  foo  client1
    Client sends Data  bar  client2
    'server1' receives 'foo' from 'connection1'
    'server1' receives 'bar' from 'connection2'

*** Keywords ***
'${server}' receives '${content}' from '${connection alias}'
    ${data}=  Server Receives data  ${server}  ${connection alias}
    Should Be Equal  ${data}  ${content}

Server and two clients are initialized
    Create tcp server  127.0.0.1  ${PORT}  server1
    Create TCP Client  client1
    Create TCP Client  client2

