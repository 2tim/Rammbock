*** Settings ***
Test Setup      TCP Server and Client are Initialized
Test Teardown   Close Connections
Default Tags    regression
Library         rammbock.Rammbock
Resource        ../resources/Messaging.txt
Resource        ../protocols/http/v1_1.txt

*** Test Cases ***
Send HTTP Get Message And Acquire Response
    [Tags]  no-ci  system-verification
    [Setup]  Create TCP Client And Connect To server  www.nokiasiemensnetworks.com  80
    Client Sends HTTP GET
    Client gets Resp
    [Teardown]  Close Client

Send and receive GET message and verify data
    Client Sends HTTP GET
    Servers Receive and verify data

Send and receive GET message and validate it
    Client Sends HTTP GET
    Server Receives HTTP GET
    Check that there are correct headers
    Check that there are correct Information Elements

Send and Receive Get and Return Response
    Clients Sends Get to Server
    Server Sends Response so Client

Send and receive GET message and try to capture wrong headers
    Client Sends HTTP GET
    Server Receives HTTP GET with wrong headers

*** Keywords ***
Servers Receive and verify data
    ${message}=  Server Receives Data
    log  ${message}
    Should Be Equal  ${message}  GET / HTTP/1.1 \r\nHost: www.nokiasiemensnetworks.com\r\nConnection: keep-alive\r\nCache-control: max-age=0\r\nUser-agent: rammbock\r\nAccept: text/html\r\n\r\n

Client gets Resp
    Create Message
    add header  Request Version
    add header  Response Code
    Client Receives Message
    ${request version}=  get header  Request Version
    ${response code}=  get header  Response Code
    Should Be Equal  ${request version}  HTTP/1.1
    Should Be True  100 <= ${response code} < 600

Client Sends HTTP GET
    Create Message
    Set HTTP GET send
    Client Sends Message

Server Receives HTTP GET
    Create Message
    Set HTTP GET rcv
    Server Receives Message

Check that there are correct headers
    ${request method}=  get header  Request Method
    ${request uri}=  get header  Request URI
    ${request version}=  get header  Request Version
    Should Be Equal  ${request method}  GET
    Should Be Equal  ${request uri}  /
    Should Be Equal  ${request version}  HTTP/1.1

Check that there are correct Information Elements
    ${host}=  Get Information Element  Host
    ${connection}=  Get Information Element  Connection
    ${cache control}=  Get Information Element  Cache-control
    ${user agent}=  Get Information Element  User-agent
    ${accept}=  Get Information Element  Accept
    Should be equal  ${host}  www.nokiasiemensnetworks.com
    Should be equal  ${connection}  keep-alive
    Should be equal  ${cache control}  max-age=0
    Should be equal  ${user agent}  rammbock
    Should be equal  ${accept}  text/html

Clients Sends Get to Server
    Client Sends HTTP GET
    Server Receives HTTP GET
    Check that there are correct headers
    Check that there are correct Information Elements

Server Sends Response so Client
    Server Creates Response and sends it To Client
    Client Receives Response

Server Creates Response and sends it To Client
    Create Message
    Set HTTP Response send
    Server Sends Message

Client Receives Response
    Create Message
    Client Receives HTTP Response
    Verify That Response is Correct

Client Receives HTTP Response
    Create Message
    Set HTTP Response rcv
    Client Receives Message

Verify That Response is Correct
    ${request version}=  get header  Request Version
    ${response code}=  get header  Response Code
    Should Be Equal  ${request version}  HTTP/1.1
    Should Be Equal  ${response code}  200
    ${connection}=  Get Information Element  Connection
    ${content-lenght}=  Get Information Element  Content-Lenght
    Should be equal  ${connection}  keep-alive
    Should be equal  ${content-lenght}  218

Client Sends Message with IE is middle of Headers
    create message
    Add String  Request URI  /
    Add Information Element  Host  www.nokiasiemensnetworks.com
    Add Header  Request Version  ${REQUEST VERSION}
    Add Delimiter  \r\n
    Client Sends Message

Server Receives Message with IE is middle of Headers
    Create message and add schema with mixed headers and IEs
    Server Receives Message
    ${request uri}=  get header  Request URI
    ${request version}=  get header  Request Version
    ${host}=  Get Information Element  Host
    Should Be Equal  ${request uri}  /
    Should Be Equal  ${request version}  HTTP/1.1
    Should be equal  ${host}  www.nokiasiemensnetworks.com

Server Receives Message and validates the data
    Create message and add schema with mixed headers and IEs
    ${data}=  Server Receives Data
    Should be equal  ${data}  / \r\nHost: www.nokiasiemensnetworks.com\r\n HTTP/1.1 \r\n\r\n

Create message and add schema with mixed headers and IEs
    Create Message
    Add Header Schema  Request URI
    Add Information Element Schema  Host
    Add Header Schema  Request Version

Server Receives HTTP GET with wrong headers
    create message
    Set HTTP GET rcv with wrong order of IEs
    Run Keyword And Expect Error  Wrong information element: 'Connection' Expected: 'Cache-control'  Server Receives Message

Set HTTP GET rcv with wrong order of IEs
    Add Header Schema  Request Method
    Add Header Schema  Request URI
    Add Header Schema  Request Version
    Add Information Element Schema  Host
    Add Information Element Schema  Cache-control
    Add Information Element Schema  Connection
    Add Information Element Schema  User-agent
    Add Information Element Schema  Accept

