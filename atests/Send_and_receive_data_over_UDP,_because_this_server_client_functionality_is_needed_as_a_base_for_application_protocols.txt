*** Settings ***
Test Setup      UDP Server and Client are initialized
Test Teardown   Close Connections
Default Tags    regression
Library         rammbock.Rammbock
Resource        resources/data_transfer.txt
Resource        resources/server_client.txt

*** Test Cases ***
Send and Receive string over UDP using physical interface
    Single Client Transfers Data
    Single Server Gets Data

Send data over UDP and modify it and return it back
    Client Sends Data To Server
    Server Sends Modified Data Pack To Client

Send multiple packets over UDP to both ways
    Client Sends Data to Server
    Server Sends Modified Data Pack To Client
    Client Sends data to server
    Server Sends Modified Data Pack To Client

Try To Bind Server To network interface which does not exist
    Try to bind server to 'foo' interface
    Exeception "IOError: cannot bind server to interface: 'foo'" should raise


*** Keywords ***
Try To Bind Server To ${interface} interface
    ${error}=  Run Keyword And Expect Error  *  Start UDP Server  ${interface}  ${PORT}
    Set Test Variable  ${EXCEPTION}  ${error}

Exeception "${except}" should raise
    Should Be Equal  ${EXCEPTION}  ${except}

Client Sends Data To Server
    Single Client Transfers Data
    Single Server Gets Data

Server Sends Modified Data Pack To Client
    Server Modifies Data
    Single Server Sends Modified Data
    Single Client Gets Data

