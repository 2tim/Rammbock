*** Settings ***
Library         Interface
Library         rammbock.Rammbock
Resource        resources/data_transfer.txt
Resource        resources/server_client.txt

*** Variables ***
${1ST_ALIAS_IP}  10.0.0.1
${1ST_ALIAS_NETMASK}  255.255.255.252
${2ND_ALIAS_IP}  10.0.0.2
${2ND_ALIAS_NETMASK}  255.255.255.252

*** Test Cases ***
Create and delete alias to interface
    Create first alias to interface '${LOOPBACK_DEVICE}'
    Delete first alias

Create and delete multiple aliases
    Create first alias to interface '${LOOPBACK_DEVICE}'
    Create second alias to interface '${LOOPBACK_DEVICE}'
    Delete first alias
    Delete second alias

Transfer data trough interface aliases
    [Setup]  Initialize server and client
    Client Tranfers Data
    Server Gets Data
    Server Modifies Data
    Server Sends Modified Data
    Client Receives Data
    [Teardown]  Close Connections and delete interfaces

*** Keywords ***
Initialize server and client
    Create first alias to interface '${LOOPBACK_DEVICE}'
    Create second alias to interface '${LOOPBACK_DEVICE}'
    Start UDP Server  ${1ST_ALIAS_NAME}  ${PORT}
    connect to UDP server  ${1ST_ALIAS_IP}  ${PORT}  ${2ND_ALIAS_NAME}

Create first alias to interface '${interface}'
    ${ifname}=  create_interface_alias  ${interface}  ${1ST_ALIAS_IP}  ${1ST_ALIAS_NETMASK}
    Set Test Variable  ${1ST_ALIAS_NAME}  ${ifname}
    ${interfacestate}=  check interface  ${ifname}
    Should Be Equal  ${interfacestate}  ${True}

Create second alias to interface '${interface}'
    ${ifname}=  create_interface_alias  ${interface}  ${2ND_ALIAS_IP}  ${2ND_ALIAS_NETMASK}
    Set Test Variable  ${2ND_ALIAS_NAME}  ${ifname}
    ${interfacestate}=  check interface  ${ifname}
    Should Be Equal  ${interfacestate}  ${True}

Delete first alias
    Del Interface  ${1ST_ALIAS_NAME}
    ${interfacestate}=  check interface  ${1ST_ALIAS_NAME}
    Should Be Equal  ${interfacestate}  ${False}

Delete second alias
    Del Interface  ${2ND_ALIAS_NAME}
    ${interfacestate}=  check interface  ${2ND_ALIAS_NAME}
    Should Be Equal  ${interfacestate}  ${False}
    
Close Connections and delete interfaces
    Close Connections
    Delete first alias
    Delete second alias
