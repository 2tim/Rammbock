*** Settings ***
Library         Interface
Library         rammbock.Rammbock
Resource        resources/data_transfer.txt
Resource        resources/server_client.txt

*** Variables ***
${1ST_ALIAS_IP}  10.0.0.1
${1ST_ALIAS_NETMASK}  255.255.255.252
${2ND_ALIAS_IP}  10.0.0.2
${2ND_ALIAS_NETMASK}  255.255.255.252

*** Test Cases ***
Create and delete alias to interface
    Create first alias to interface 'lo'
    Delete first alias

Create and delete multiple aliases
    Create first alias to interface 'lo'
    Create second alias to interface 'lo'
    Delete first alias
    Delete second alias

Transfer data trough interface aliases
    [Setup]  Initialize server and client
    Client Sends String
    Server Receives String
    Server Modifies String
    Server Sends Modified String
    Client Receives String Over
    [Teardown]  Close Connections

*** Keywords ***
Initialize server and client
    #Create first alias to interface 'lo'
    #Create second alias to interface 'lo'
    Start Server  ${PHYSICAL_INTERFACE}  ${PORT}
    rammbock.Rammbock.connect to server  ${HOST}  ${PORT}

Create first alias to interface '${interface}'
    ${ifname}=  create_interface_alias  ${interface}  ${1ST_ALIAS_IP}  ${1ST_ALIAS_NETMASK}
    Set Test Variable  ${1ST_ALIAS_NAME}  ${ifname}
    ${interfacestate}=  check interface  ${ifname}
    Should Be Equal  ${interfacestate}  ${True}

Create second alias to interface '${interface}'
    ${ifname}=  create_interface_alias  ${interface}  ${2ND_ALIAS_IP}  ${2ND_ALIAS_NETMASK}
    Set Test Variable  ${2ND_ALIAS_NAME}  ${ifname}
    ${interfacestate}=  check interface  ${ifname}
    Should Be Equal  ${interfacestate}  ${True}

Delete first alias
    Del Interface  ${1ST_ALIAS_NAME}
    ${interfacestate}=  check interface  ${1ST_ALIAS_NAME}
    Should Be Equal  ${interfacestate}  ${False}

Delete second alias
    Del Interface  ${2ND_ALIAS_NAME}
    ${interfacestate}=  check interface  ${2ND_ALIAS_NAME}
    Should Be Equal  ${interfacestate}  ${False}
