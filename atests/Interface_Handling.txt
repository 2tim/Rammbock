*** Settings ***
Default Tags    no-ci
Library         Interface
Resource        resources/data_transfer.txt
Resource        resources/server_client.txt

*** Variables ***
${1ST_ALIAS_IP}  10.0.0.1
${1ST_ALIAS_NETMASK}  255.255.255.252
${2ND_ALIAS_IP}  10.0.0.2
${2ND_ALIAS_NETMASK}  255.255.255.252

*** Test Cases ***
Create and delete alias to interface
    Create first alias to interface '${LOOPBACK_DEVICE}'
    Delete first alias

Create and delete multiple aliases
    Create first alias to interface '${LOOPBACK_DEVICE}'
    Create second alias to interface '${LOOPBACK_DEVICE}'
    Delete first alias
    Delete second alias

Transfer data through interface aliases
    [Setup]  Initialize server and client
    Single Client Transfers Data
    Single Server Gets Data
    Server Modifies Data
    Single Server Sends Modified Data
    Single Client Gets Data
    [Teardown]  Close Connections and delete interfaces

Delete all interfaces
    Create first alias to interface '${LOOPBACK_DEVICE}'
    Create second alias to interface '${LOOPBACK_DEVICE}'
    Del all aliases
    Interface alias should not exist  ${1ST_ALIAS_NAME}  ${1ST_ALIAS_IP}
    Interface alias should not exist  ${2ND_ALIAS_NAME}  ${2ND_ALIAS_IP}

*** Keywords ***
Initialize server and client
    Create first alias to interface '${LOOPBACK_DEVICE}'
    Create second alias to interface '${LOOPBACK_DEVICE}'
    Start UDP Server  ${1ST_ALIAS_IP}  ${PORT}
    Create UDP Client
    connect to UDP server  ${1ST_ALIAS_IP}  ${PORT}

Create first alias to interface '${interface}'
    ${ifname}=  create interface alias  ${interface}  ${1ST_ALIAS_IP}  ${1ST_ALIAS_NETMASK}
    Set Test Variable  ${1ST_ALIAS_NAME}  ${ifname}
    Interface alias should exist  ${ifname}  ${1ST_ALIAS_IP}

Create second alias to interface '${interface}'
    ${ifname}=  create_interface_alias  ${interface}  ${2ND_ALIAS_IP}  ${2ND_ALIAS_NETMASK}
    Set Test Variable  ${2ND_ALIAS_NAME}  ${ifname}
    Interface alias should exist  ${ifname}  ${2ND_ALIAS_IP}

Delete first alias
    Del Alias  ${1ST_ALIAS_NAME}  ${1ST_ALIAS_IP}
    Interface alias should not exist  ${1ST_ALIAS_NAME}  ${1ST_ALIAS_IP}

Delete second alias
    Del Alias  ${2ND_ALIAS_NAME}  ${2ND_ALIAS_IP}
    Interface alias should not exist  ${2ND_ALIAS_NAME}  ${2ND_ALIAS_IP}

Interface alias should exist
    [Arguments]  ${alias name}  ${alias ip}
    ${interfacestate}=  check interface for ip  ${alias name}  ${alias ip}
    Should Be Equal  ${interfacestate}  ${True}

Interface alias should not exist
    [Arguments]  ${alias name}  ${alias ip}
    ${interfacestate}=  check interface for ip  ${alias name}  ${alias ip}
    Should Be Equal  ${interfacestate}  ${False}

Close Connections and delete interfaces
    Close Connections
    Delete first alias
    Delete second alias

