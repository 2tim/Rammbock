*** Settings ***
Test Setup      Create Two TCP Servers And Clients
Test Teardown   Delete Two Servers And Clients And Verify Deletion
Default Tags    regression
Resource        resources/data_transfer.txt
Resource        resources/server_client.txt

*** Variables ***
${LOCALHOST_IP}  127.0.0.1
${SERVER_NAME1}  testiserveri1
${SERVER_NAME2}  testiserveri2
${DEFAULT_PORT1}  3011
${DEFAULT_PORT2}  23205
${CLIENT_NAME1}  testiclient1
${CLIENT_NAME2}  testiclient2

*** Test Cases ***
Create and delete two instances of Servers
    Server Should Be Running  ${SERVER_NAME1}
    Server Should Be Running  ${SERVER_NAME2}

Create and delete two instances of Clients
    Client Should Be Running  ${CLIENT_NAME1}
    Client Should Be Running  ${CLIENT_NAME2}

Send data between multiple Client and Server instances
    Client Connects to TCP server  ${HOST}  ${DEFAULT_PORT1}  ${CLIENT_NAME1}
    Server Accepts TCP Connection  ${SERVER_NAME1}
    Client Connects to TCP server  ${HOST}  ${DEFAULT_PORT2}  ${CLIENT_NAME2}
    Server Accepts TCP Connection  ${SERVER_NAME2}
    Client Transfers Data  ${CLIENT_NAME1}
    Server Receives packet  ${SERVER_NAME1}
    Client Transfers Data  ${CLIENT_NAME2}
    Server Receives packet  ${SERVER_NAME2}
    Server Sends packet  ${SERVER_NAME1}
    Client Receives Packet  ${CLIENT_NAME1}
    Server Sends packet  ${SERVER_NAME2}
    Client Receives Packet  ${CLIENT_NAME2}

Create Two UDP Server Instances without name
    [Setup]
    Create First UDP Server
    Create Second UDP Server and Expect Error
    [Teardown]  Delete Server

Create Two TCP Server Instances without name
    [Setup]
    Create First TCP Server
    Create Second TCP Server and Expect Error
    [Teardown]  Delete Server

Create Two SCTP Server Instances without name
    [Tags]  no-ci  linux-only
    [Setup]
    Create First SCTP Server
    Create Second SCTP Server and Expect Error
    [Teardown]  Delete Server

Create Two UDP Client instances without names
    Create First UDP Client
    Create Second UDP Client and Expect Error
    [Teardown]  Delete Client

Create Two TCP Client instances without names
    Create TCP Client  ip=${LOCALHOST_IP}
    Run Keyword And Expect Error  There is already one TCP Client created. You need to specify a unique name for a new clients  Create TCP Client  ip=${LOCALHOST_IP}
    [Teardown]  Delete Client

Create Two SCTP Client instances without names
    [Tags]  no-ci  linux-only
    Create Sctp Client  ip=${LOCALHOST_IP}
    Run keyword and expect error  There is already one SCTP Client created. You need to specify a unique name for a new clients  Create SCTP Client  ip=${LOCALHOST_IP}
    [Teardown]  Delete Client

Send message from last created client
    [Setup]  Create First UDP Server
    Create UDP Client  some random name for client  ${LOCALHOST_IP}
    Client Connects To Udp Server  ${LOCALHOST_IP}  ${DEFAULT_PORT1}
    Client Sends Data  foobar
    ${data}=  Server Receives Data
    Should Be Equal As Strings  ${data}  foobar
    [Teardown]  Delete server and client

Receive Message from last created Server
    [Tags]  ${EMPTY}
    [Setup]  Create first UDP Client
    Create UDP SERVER  ${LOCALHOST_IP}  ${DEFAULT_PORT1}  some random name
    Client Connects To Udp Server  ${LOCALHOST_IP}  ${DEFAULT_PORT1}
    client sends data  foobar
    server receives data
    Server Sends Data  foobar
    ${data}=  Client Receives Data
    Should Be Equal As Strings  ${data}  foobar
    [Teardown]  Delete server and client

Latest UDP servers and client are used when sending data
    [Tags]  ${EMPTY}
    Create Two UDP Servers and Clients
    Send Data from the client to the server
    last created server and client are used

Latest TCP servers and client are used when sending data
    [Tags]
    Create Two TCP Servers and Clients
    Send Data from the client to the server
    last created server and client are used

Latest SCTP servers and client are used when sending data
    [Tags]
    Create Two SCTP Servers and Clients
    Send Data from the client to the server
    last created server and client are used

*** Keywords ***
Create Two TCP Servers And Clients
    Create TCP Server  ${LOCALHOST_IP}  ${DEFAULT_PORT1}  ${SERVER_NAME1}
    Create TCP Server  ${LOCALHOST_IP}  ${DEFAULT_PORT2}  ${SERVER_NAME2}
    Create TCP Client  ${CLIENT_NAME1}
    Create TCP Client  ${CLIENT_NAME2}

Delete Two Servers And Clients And Verify Deletion
    Delete Server  ${SERVER_NAME1}
    Run Keyword And Expect Error  *  Server Should Be Running  ${SERVER_NAME1}
    Delete Server  ${SERVER_NAME2}
    Run Keyword And Expect Error  *  Server Should Be Running  ${SERVER_NAME2}
    Delete Client  ${CLIENT_NAME1}
    Run Keyword And Expect Error  *  Server Should Be Running  ${CLIENT_NAME1}
    Delete Client  ${CLIENT_NAME2}
    Run Keyword And Expect Error  *  Server Should Be Running  ${CLIENT_NAME2}

Create First UDP Server
    Create UDP Server  ${LOCALHOST_IP}  ${DEFAULT_PORT1}

Create Second UDP Server and Expect Error
    Run Keyword And Expect Error  There is already one UDP Server created. You need to specify a unique name for a new server  Create UDP Server  ${LOCALHOST_IP}  ${DEFAULT_PORT2}

Create First TCP Server
    Create TCP Server  ${LOCALHOST_IP}  ${DEFAULT_PORT1}

Create Second TCP Server and Expect Error
    Run Keyword And Expect Error  There is already one TCP Server created. You need to specify a unique name for a new server  Create TCP Server  ${LOCALHOST_IP}  ${DEFAULT_PORT2}

Create First SCTP Server
    Create SCTP Server  ${LOCALHOST_IP}  ${DEFAULT_PORT1}

Create Second SCTP Server and Expect Error
    Run Keyword And Expect Error  There is already one SCTP Server created. You need to specify a unique name for a new server  Create SCTP Server  ${LOCALHOST_IP}  ${DEFAULT_PORT2}

Create First UDP Client
    Create Udp Client  ip=${LOCALHOST_IP}

Create Second UDP Client and Expect Error
    Run Keyword And Expect Error  There is already one UDP Client created. You need to specify a unique name for a new clients  Create Udp Client  ip=${LOCALHOST_IP}

Delete server and client
    Delete server
    Delete client

