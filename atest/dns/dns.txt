*** Settings ***
Suite teardown    Reset Rammbock
Library    Rammbock
Library    DNSLibrary.py


*** Test cases ***
DNS Query
    define dns protocol
    dns A query with one domain
    ${msg}=    Get message

dns answer
    define dns protocol
    dns answer with one domain and IPv4 answer
    ${msg}=    Get message


*** Keywords ***
dns answer with one domain and IPv4 answer
    new message DNS    questions=1    answer_rrs=1
    query for google.com
    answer for google.com

answer for google.com
    Value    answers[0].type    1
    Value    answers[0].class    1
    Value    answers[0].time_to_live    1200
    Value    answers[0].data_length    4
    IP Value    answers[0].address    192.168.0.1

dns A query with one domain
    new message    DNS    questions=1
    query for google.com

query for google.com
    Label sequence value    queries[0].query.name    google.com
    Value    queries[0].query.type    0x0001
    Value    queries[0].query.class   0x0001

Label sequence value
    [arguments]    ${field}    ${domain}
    ${converted}=    convert to label sequence    ${domain}
    Value    ${field}    ${converted}

define dns protocol
    new protocol    DNS
    dns header
    Array     questions        dns_query    queries
    Array     answer_rrs       dns_answer    answers
    Array     authority_rrs    authority_rr    authority_rrs
    Array     additional_rrs   additional_rr    additional_rrs
    end protocol

dns header
    u16    transaction_id
    dns flags
    u16    questions        0
    u16    answers_rrs      0
    u16    authority_rrs    0
    u16    additional_rrs   0

dns query
    new struct    DNS query    query
    label sequence    name
    u16    type
    u16    class
    end struct

dns answer
    new struct    DNS answer    answer
    u16    name    0xc00c
    u16    type
    u16    class
    u32    time_to_live
    u16    data_length
    Uint    data_length    address
    end struct

Label sequence
    [arguments]    ${name}
    Chars    *    ${name}    terminator=0x00

dns flags
    new binary container    flags
    bin    1    response
    bin    4    opcode       0
    bin    1    spare        0
    bin    1    truncated    0
    bin    1    recursion_desired    0
    bin    1    spare        0
    bin    1    Z        0
    bin    1    spare        0
    bin    1    non_authenticated_data        0
    bin    4    spare        0
    end binary container
