*** Settings ***
Test Setup        Create server and client, and define protocol
Test Teardown     Reset Rammbock
Default Tags      regression
Resource          Protocols.txt

*** Variables ***
${GTP CONTROL PORT}                 2123
${LOCALHOST IP}                     127.0.0.1
${CREATE SESSION REQUEST}           32
${IMSI_type}                        1
${MSISDN_type}                      76
${ULI_type}                         86
${Serving network_type}             83
${Rat type_type}                    82
${Indication_type}                  77
${APN_type}                         71
${Selection mode_type}              128
${APN Restriction_type}             127
${PDN Type_Type}                    99
${PDN Address Allocation_type}      79
${Aggregate maximum bit rate_type}  72
${Bearer context_type}              93
${Recovery_type}                    3
${Ue time zone_type}                114
${Charging characteristics_type}    95
${F-TEID_type}                      87
${Bearer level qos_type}            80
${EPS bearer id_type}               73


*** Test Cases ***
Create Session Request
    Create and send Create Session Request
    Receive and Validate Create Session Request

*** Keywords ***
Create and send Create Session Request
    Create Session Request
    client sends message   header:sequence number:48

Receive and Validate Create Session Request
    Create Session Request
    ${msg}=    Server Receives message    header:sequence number:48

Create server and client, and define protocol
    define gtpv2 protocol
    start udp server    ${LOCALHOST IP}    ${GTP CONTROL PORT}    protocol=gtpv2
    start udp client    ip=${LOCALHOST IP}    protocol=gtpv2
    connect    ${LOCALHOST IP}    ${GTP CONTROL PORT}

define gtpv2 protocol
    new protocol    gtpv2
    u8    flags    72
    u8    message type
    u16   message length
    u32   tunnel endpoint identifier    0
    u24   sequence number
    u8    spare    0
    pdu    message length-12
    end protocol

Create Session Request
    new message    create session request    gtpv2    message type:${CREATE SESSION REQUEST}
    IE  IMSI        instance.value:0x01    value.imsi.value:262120000000001
    IE  MSISDN
    IE  ULI         value.flags:0b00011000    value.tai.mcc.value:262  value.tai.mnc.value:12       value.ecgi.mcc.value:262    value.ecgi.mnc.value:12    value.ecgi.eci.ecgi:234    value.tai.tracking_area_code:1
    IE  Serving network   value.mcc.value:262    value.mnc.value:12
    IE  Rat type    value.rat_type:6
    IE  Indication    value.fields.DAF:1
    Named IE    f-teid1     F-TEID    value.f-teid_ipv4:0xc0a8700a    value.values.interface type:7
    Named IE    f-teid2     F-TEID    instance.value:1    value.f-teid_ipv4:0x0a66b6b8    value.values.interface type:10
    IE  APN     value.access_point_name:sgw.foo.com.mnc012.mcc262.gprs
    IE  Selection mode
    IE  PDN Type
    IE  PDN Address Allocation
    IE  APN Restriction
    IE  Aggregate maximum bit rate    value.ambr_uplink:2    value.ambr_downlink:1
    IE  Bearer context    value.eps_bearer_id.value.epsbid.value:5    value.bearer_level_qos.value.arp.pl:1   value.bearer_level_qos.value.label:9
    IE  Recovery        value.recovery:1
    IE  Ue time zone
    IE  charging characteristics

charging characteristics
    u16    charging_characteristic    0x3200

Ue time zone
    new binary container    timezone
    bin   6    spare    0b111111
    bin   2    value    0
    end binary container
    new binary container    dst
    bin   6    spare   0
    bin   2    value   0
    end binary container

Recovery
    u8    recovery

Bearer context
    IE       eps_bearer_id
    IE       bearer_level_qos

EPS bearer id
    new binary container    epsbid
    bin    4    spare   0
    bin    4    value
    end binary container

Bearer level qos
    new binary container   arp
    bin   1    spare   0
    bin   1    pci    0
    bin   4    pl
    bin   1    spare_2   0
    bin   1    pvi     0
    end binary container
    u8    label
    u40    mbr_uplink      0
    u40    mbr_downlink    0
    u40    gbr_uplink      0
    u40    gbr_downlink    0

Aggregate maximum bit rate
    u32    ambr_uplink
    u32    ambr_downlink

APN Restriction
    u8    apn_restriction   0

PDN Address Allocation
    PDN Type
    u32    pdn_address_and_prefix   0

PDN Type
    new Binary container    pdn_type
    bin    5    spare    0
    bin    3    value    1
    end binary container

Selection mode
    new Binary container    selection_mode
    bin    6    spare    0b111111
    bin    2    value    0
    end binary container

APN
    chars  *  access_point_name

F-TEID
    [Arguments]    @{args}
    new binary container    values
    bin    1    v4    1
    bin    1    v6    0
    bin    6    interface type
    end binary container
    u32    teid_gre_key    0x00
    u32    f-teid_ipv4

Indication
    new binary container    fields
    Bin    1    DAF     0
    Bin    1    DTF     0
    Bin    1    HI      0
    Bin    1    DFI     0
    Bin    1    OI      0
    Bin    1    ISRSI   0
    Bin    1    ISRAI   0
    Bin    1    SGWCI   0
    Bin    1    SQCI    0
    Bin    1    UIMSI   0
    Bin    1    CFSI    0
    Bin    1    CRSI    0
    Bin    1    PS      0
    Bin    1    PT      0
    Bin    1    SI      0
    Bin    1    MSV     0
    Bin    3    SPARE   0
    Bin    1    S6AF    0
    Bin    1    S4AF    0
    Bin    1    MBMDT   0
    Bin    1    ISRAU   0
    Bin    1    CCRSI   0
    end binary container

Rat type
    u8    rat_type

Serving network
    [Arguments]    @{arguments}
    new tbcd container    mcc
    tbcd    3    value
    end tbcd container
    new tbcd container    mnc
    tbcd    2    value
    end tbcd container

ULI
    [Arguments]       @{args}
    u8    flags
    TAI
    ECGI

ECGI
    new struct    E-UTRAN Cell Global Identifier    ecgi
    single value tbcd container    mcc    3        ${none}
    single value tbcd container    mnc    2        ${none}
    new binary container   eci
    Bin    4    spare    0
    bin    28   ecgi
    end binary container
    end struct

TAI
    new struct    tai    tai
    single value tbcd container    mcc    3        ${none}
    single value tbcd container    mnc    2        ${none}
    u16    tracking_area_code
    end struct

IMSI
    new tbcd container    imsi
    tbcd    15    value
    end tbcd container

MSISDN
    new tbcd container    msisdn
    tbcd    3    country_code       358
    tbcd    *   address_digits      6100000000001
    end tbcd container

IE
    [Arguments]    ${name}    @{values}
    Named IE    ${name}    ${name}    @{values}

Named IE
    [Arguments]    ${field name}  ${name}    @{values}
    ${type}     Find type if defined or empty    ${name}
    New struct    IE    ${field name}    @{values}
    IE header   ${name}    ${type}
    IE value    ${name}
    End struct

Find type if defined or empty
    [Arguments]    ${name}
    ${type} =      Get variable value    ${${name}_type}     ${empty}
    [return]      ${type}

IE header
    [Arguments]    ${name}    ${type}
    u8    ie type    ${type}
    u16    ie_length
    new binary container    instance
    bin    4    spare    0
    bin    4    value    0
    end binary container

IE Value
    [Arguments]    ${kw}    ${name}=value
    new struct    Container    ${name}    length=ie_length
    run keyword    ${kw}
    end struct

ADDRESS
    [Arguments]    ${value}
    IE    address    54    Chars    ie length    ${value}

single value tbcd container
    [Arguments]    ${name}    ${len}    ${value}
    new tbcd container    ${name}
    tbcd   ${len}    value    ${value}
    end tbcd container
