*** Settings ***
Test Setup        Define Simple Protocol
Test Teardown     Reset rammbock
Default Tags      regression
Resource          template_resources.txt


*** Test Cases ***
Save templates
    Save single and double templates
    Load template    single
    ${msg}=          Get message
    Should be equal as integers    ${msg.single.int}   1
    Load template    double
    ${msg}=          Get message
    Should be equal as integers    ${msg.double_1.int}   22

Save templates and field values
    Save single and double templates with values
    Load template    single
    ${msg}=          Get message
    Should be equal as integers    ${msg.single.int}   42
    Load template    double
    ${msg}=          Get message
    Should be equal as integers    ${msg.double_1.int}   4242

Load template with header parameters
    Save single and double templates
    Load template     single       field:10
    ${msg}=           Get message
    Should be equal as integers    ${msg._header.field}    10
    
Load template with header parameters does not alter the original template
    Save single and double templates
    Load template     single       field:10
    Load template     single
    ${msg}=           Get message
    Should be equal as integers    ${msg._header.field}    16

Load template with header parameters and field values
    Save single and double templates with values
    Load template     single       field:10
    ${msg}=           Get message
    Should be equal as integers    ${msg._header.field}    10
    Should be equal as integers    ${msg.single}           42


*** Keywords ***
Save single and double templates
    Single valued
    Save template    single
    Double valued
    Save template    double

Single valued
    New message    SingleRequest    Example
    u32    single    1

Double valued
    New message    DoubleRequest    Example
    u16    double_1  22
    u16    double_2  2222

Save single and double templates with values
    Single valued
    value   single   42
    Save template    single
    Double valued
    value  double_1  4242
    Save template    double
