*** Settings ***
Library     my_handler.py
Library     Process
Resource    async_resources.txt
Test Setup     Setup protocol, nodes, and define templates
Test teardown    Teardown rammbock and increment port numbers
Default tags    regression

*** Test cases ***
Register an auto reply
    Load Template   sample
    Reset received messages
    Set client handler  my_handler.handle_sample
    Server sends sample message
    Server sends another message
    Client receives another message
    Handler should have been called with sample message
    Message cache should be empty
Respond to an asynchronous message
    Load Template   sample
    Reset received messages
    Set client handler  my_handler.respond_to_sample
    Server sends sample message
    Server sends another message
    Client receives another message
    Server should receive response to sample
Multiple clients
    [Setup]   Setup protocol, server, two clients, and define templates
    Load Template   sample
    Reset received messages
    Set client handler  my_handler.respond_to_sample    name=ExampleClient2
    Server sends sample message   Connection1
    Server sends another message  Connection1
    Client receives another message    name=ExampleClient1
    Sample message should be in cache   name=ExampleClient1
    Server sends sample message   Connection2
    Server sends another message  Connection2
    Client receives another message    name=ExampleClient2
    Server should receive response to sample
Asynchronous messages on background
    [Setup]    Setup protocol, one client, and define templates
    Load Template   sample
    Reset received messages
    Set client handler  my_handler.respond_to_sample     header_filter=messageType
    Load template    another
    client sends message
    Wait until keyword succeeds     2s  0.1s  Handler should have been called with sample message
    client receives message    header_filter=messageType
    [Teardown]     Get background results and reset
Register an auto reply to work on background
    Load Template   sample
    Reset received messages
    Set client handler  my_handler.handle_sample
    Server sends sample message
    Wait until keyword succeeds   2s  0.1s   Handler should have been called with sample message
    Message cache should be empty

*** Variables ***
${SOURCEDIR}=   ${CURDIR}${/}..${/}..${/}src
${BACKGROUND FILE}=    ${CURDIR}${/}background_server.txt

*** Keywords ***
Get background results and reset
    ${res}=    Terminate Process
    Log    STDOUT:\n${res.stdout}\nSTDERR:\n${res.stderr}
    Teardown rammbock and increment port numbers
Setup protocol, one client, and define templates
    Define Example protocol
    Define templates
    Start background process
    Start TCP client    127.0.0.1   45555   name=client   protocol=Example
    Sleep   1   # Unfortunately if connect fails, the same socket can not be reused. So polling becomes difficult
    Connect     127.0.0.1   44455
Setup protocol, server, two clients, and define templates
    Define protocol, start tcp server and two clients    protocol=Example
    Define templates
Setup protocol, nodes, and define templates
    Setup protocol, TCP server, and client
    Define Templates
Server sends sample message    [Arguments]    ${connection}=
    Load Template   sample
    Server sends message    connection=${connection}
Server sends another message   [Arguments]    ${connection}=
    Load Template   another
    Server sends message    connection=${connection}
Client receives another message
    [Arguments]     ${name}=
    Load Template   another
    Client receives message   name=${name}  header_filter=messageType
Handler should have been called with sample message
    ${rcvd msgs}   Get rcvd msg
    Length should be   ${rcvd_msgs}     1
    ${msg}=    Set variable   ${rcvd msgs[0]}
    Should be equal   ${msg._header.messageType.hex}     0x0042
Server should receive response to sample
    Load Template   sample response
    Server receives message     timeout=1
Message cache should be empty
    ${my_count}=     Get Client Unread Messages Count
    Should be Equal as integers     ${my_count}     0
Sample message should be in cache
    [Arguments]     ${name}=
    ${my_count}=     Get Client Unread Messages Count   ${name}
    Should be Equal as integers     ${my_count}     1
Start background process
    ${process}=    Start process  pybot  --variable  BACKGROUND:True   --pythonpath  ${SOURCEDIR}  --outputdir  ${TEMPDIR}  ${BACKGROUND FILE}  shell=True
